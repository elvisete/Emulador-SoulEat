// ===== Script rAthena ===========================================
// = Habilidades de Platina
// ===== Descrição: ===============================================
// = Único NPC que atribui habilidades de missão para todas as classes
// ===== Traduzido por: ===============================================
// = Guru
// ===== Comentários adicionais: ===================================
// = 1.0 Versão inicial. [Keichii] [DarkChild]
// = 2.0 Classes avançadas adicionadas. [ShadowLady]
// = 2.1 Adicionadas classes de bebês. [Midas]
// = 2.2 Verificações de trabalho simplificadas. [Silentdragon]
// = 2,3 Thehell? Limpei o script, removeu a maldade. Também adicionou habilidades de renascimento. [Spre]
// = 2.4 Adicionadas habilidades avançadas. [Spre]
// = 2.5 Diálogo adicionado para se o usuário não for nenhum dos trabalhos listados. [Kisuka]
// = 2.6 2ª classes avançadas corrigidas não sendo capazes de obter suas habilidades avançadas. [Ancyker]
// = 2.7 Reescrito para dar todas as habilidades corretas a todas as classes adequadas. [Paradox924X]
// = 2.8 Condicionais agrupados substituídos por switch para incluir todos os tipos superiores. [Euphy]
// = 2.9 Adicionado MC_CARTDECORATE para a classe Merchant. [mazvi]
// = 3.0 Migrar / integrar para funções globais. [mazvi]
//============================================================

prontera,144,177,6	script	Habilidades de Platina	94,{
	mes "^FF1493[ Habilidades de Platina ]^000000";
	mes (gettime(3)>= 6&&gettime(3)<= 12?"Bom-dia":(gettime(3)>=13&&gettime(3)<=18?"Boa-tarde":"Boa-noite"))+", ^008aff"+strcharinfo(0)+"^000000 !";
	mes "Eu posso te ensinar algumas habilidades muito importantes.";
	mes "Você gostaria de aprender?";
	next;
	if (select("Sim, por favor:Não, obrigado") == 2) {
		mes "^FF1493[ Habilidades de Platina ]^000000";
		mes "Tenha um bom dia... >.>";
		close;
	}
	skill "NV_FIRSTAID",1,SKILL_PERM;
	switch (BaseClass) {
	case Job_Novice:
		if (Class != Job_SuperNovice)
			skill "NV_TRICKDEAD",1,SKILL_PERM;
		break;
	case Job_Swordman:
		skill "SM_MOVINGRECOVERY",1,SKILL_PERM;
		skill "SM_FATALBLOW",1,SKILL_PERM;
		skill "SM_AUTOBERSERK",1,SKILL_PERM;
		break;
	case Job_Mage:
		skill "MG_ENERGYCOAT",1,SKILL_PERM;
		break;
	case Job_Archer:
		skill "AC_MAKINGARROW",1,SKILL_PERM;
		skill "AC_CHARGEARROW",1,SKILL_PERM;
		break;
	case Job_Acolyte:
		skill "AL_HOLYLIGHT",1,SKILL_PERM;
		break;
	case Job_Merchant:
		skill "MC_CARTREVOLUTION",1,SKILL_PERM;
		skill "MC_CHANGECART",1,SKILL_PERM;
		skill "MC_LOUD",1,SKILL_PERM;
		skill "MC_CARTDECORATE",5,SKILL_PERM;
		break;
	case Job_Thief:
		skill "TF_SPRINKLESAND",1,SKILL_PERM;
		skill "TF_BACKSLIDING",1,SKILL_PERM;
		skill "TF_PICKSTONE",1,SKILL_PERM;
		skill "TF_THROWSTONE",1,SKILL_PERM;
		break;
	default:
		break;
	}
	switch (BaseJob) {
	case Job_Knight:
		skill "KN_CHARGEATK",1,SKILL_PERM;
		break;
	case Job_Priest:
		skill "PR_REDEMPTIO",1,SKILL_PERM;
		break;
	case Job_Wizard:
		skill "WZ_SIGHTBLASTER",1,SKILL_PERM;
		break;
	case Job_Blacksmith:
		skill "BS_UNFAIRLYTRICK",1,SKILL_PERM;
		skill "BS_GREED",1,SKILL_PERM;
		break;
	case Job_Hunter:
		skill "HT_PHANTASMIC",1,SKILL_PERM;
		break;
	case Job_Assassin:
		skill "AS_SONICACCEL",1,SKILL_PERM;
		skill "AS_VENOMKNIFE",1,SKILL_PERM;
		break;
	case Job_Crusader:
		skill "CR_SHRINK",1,SKILL_PERM;
		break;
	case Job_Monk:
		skill "MO_KITRANSLATION",1,SKILL_PERM;
		skill "MO_BALKYOUNG",1,SKILL_PERM;
		break;
	case Job_Sage:
		skill "SA_CREATECON",1,SKILL_PERM;
		skill "SA_ELEMENTWATER",1,SKILL_PERM;
		skill "SA_ELEMENTGROUND",1,SKILL_PERM;
		skill "SA_ELEMENTFIRE",1,SKILL_PERM;
		skill "SA_ELEMENTWIND",1,SKILL_PERM;
		break;
	case Job_Rogue:
		skill "RG_CLOSECONFINE",1,SKILL_PERM;
		break;
	case Job_Alchemist:
		skill "AM_BIOETHICS",1,SKILL_PERM;
		break;
	case Job_Bard:
		skill "BA_PANGVOICE",1,SKILL_PERM;
		break;
	case Job_Dancer:
		skill "DC_WINKCHARM",1,SKILL_PERM;
		break;
	default:
		break;
	}
	mes "^FF1493[ Habilidades de Platina ]^000000";
	mes "Pronto vocês acaba de aprender as suas novas Habilidades de Platina.";
	close;
}
