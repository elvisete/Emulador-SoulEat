//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

// Christopher: Ferreiro de Geffen
//============================================================
geffen_in,110,172,0	script	Christopher#1	63,{
	mes "[Christopher Guillenrow]";
	mes "Bem-vindo à Oficina de Christopher. Aqui você pode obter tudo o que precisa para forjar. Qual é o seu negócio";
	mes "que o traz até mim?";
	next;
	switch(select("Comprar Bigorna:Comprar Item de Forja:Comprar Metal:Purificar Minérios Brutos:Cancelar")) {
	case 1:
		mes "[Christopher Guillenrow]";
		mes "Uma bigorna melhor dá a você uma chance maior de fazer armas melhores, sabia? Mas elas vão custar mais zeny. Apenas coloque isso para fora do peito e compre o que se adequa melhor aos seus propósitos, jovem.";
		next;
		switch(select("Bigorna - 30.000 zeny:Bigorna de Oridecon - 120.000 zeny:Bigorna Dourada - 300.000 zeny:Melhor Bigorna que as outras.:Cancelar.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 986, 1; // Bigorna
			Zeny = Zeny - 30000;
			mes "[Christopher Guillenrow]";
			mes "Esta é a mais barata, mas eficiente o suficiente para forjar a maioria dos itens. Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 987, 1; // Bigorna de Oridecon
			Zeny = Zeny - 120000;
			mes "[Christopher Guillenrow]";
			mes "Sim, amigo, você tem olho para a bigorna. Esta deve ser a bigorna adequada para um Ferreiro, não é? Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 988, 1; // Bigorna Dourada
			Zeny = Zeny - 300000;
			mes "[Christopher Guillenrow]";
			mes "Esta é a melhor entre todas as coisas na minha oficina! Com isso, você pode dominar o mundo dos Ferreiros! Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 4:
			mes "[Christopher Guillenrow]";
			mes "Bem, desculpe. Mas eu não tenho nada mais difícil do que a Bigorna Dourada.";
			next;
			mes "[Christopher Guillenrow]";
			mes "Eu acho que 'Ringgel', o Fabricante Lendário de Bigornas, teria uma. Mas, acho que você não conseguirá encontrá-lo, embora ele esteja em algum lugar deste mundo.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Certo, sinta-se à vontade para vir a qualquer momento, sempre que precisar. Adeus.";
			close;
		}
	case 2:
		mes "[Christopher Guillenrow]";
		mes "Um ferreiro respeitável usa ferramentas de qualidade. Você pode se tornar um deles com minhas coisas. Escolha o que quiser.";
		next;
		switch(select("Mini-Fornalha - 150 zeny:Martelo de Ferro - 1000 zeny:Martelo Dourado - 3000 zeny:Martelo de Oridecon - 5000 zeny:Cancelar.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "É uma ferramenta muito necessária para refinar metal! Então, quantas você deseja comprar? Se quiser desistir, basta digitar o número '0'.";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "Sim, o negócio está cancelado. Adeus.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você pode comprar até 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 150;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			if (checkweight(612, .@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Você parece não ter espaço suficiente no inventário. Coloque algumas coisas no seu Armazém Kafra, por que não?";
				close;
			}
			getitem 612, .@input; // Fornalha Portátil
			Zeny = Zeny - .@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 613, 1; // Martelo de Ferro
			Zeny = Zeny - 1000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 614, 1; // Martelo Dourado
			Zeny = Zeny - 3000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			getitem 615, 1; // Martelo de Oridecon
			Zeny = Zeny - 5000;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 5:
			mes "[Christopher Guillenrow]";
			mes "Sinta-se à vontade para vir a qualquer momento, sempre que precisar. Adeus.";
			close;
		}
	case 3:
		mes "[Christopher Guillenrow]";
		mes "Eu preparo todos os metais, e apenas os de alta qualidade, é claro. Agora, qual você precisa?";
		next;
		switch(select("Fracon - 200z.:Emveretarcon - 1000z.:Cancelar.")) {
		case 1:
			mes "[Christopher Guillenrow]";
			mes "Então, quantos você deseja comprar? Se você não quiser nada, basta digitar o número '0'.";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "A transação";
					mes "foi cancelada.";
					mes "Adeus.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você pode comprar até 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 200;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Você não tem dinheiro suficiente. Você sabe que não posso vender por um preço mais baixo... Você sabe como a patroa reclama sobre Zeny.";
				close;
			}
			if (checkweight(1010, .@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Você parece não ter espaço para carregar nada novo. Que tal colocar algumas coisas no Armazém Kafra e voltar.";
				close;
			}
			getitem 1010, .@input; // Fracon
			Zeny = Zeny - .@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar.";
			close;
		case 2:
			mes "[Christopher Guillenrow]";
			mes "Então, quantos você deseja comprar? Se você não quiser nada, basta digitar o número '0'.";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[Christopher Guillenrow]";
					mes "A transação";
					mes "foi cancelada.";
					mes "Adeus.";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[Christopher Guillenrow]";
					mes "Você pode comprar até 500, ou menos.";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 1000;
			if (Zeny < .@sell) {
				mes "[Christopher Guillenrow]";
				mes "Acho que não posso deixar você ter isso com o zeny que tem. Eu não posso perder meu dinheiro por sua causa.";
				close;
			}
			if (checkweight(1011, .@input) == 0) {
				mes "[Christopher Guillenrow]";
				mes "Meu amigo... Parece que você não tem espaço no inventário. Que tal colocar algumas coisas no Armazém Kafra primeiro?";
				close;
			}
			getitem 1011, .@input; // Emveretarcon
			Zeny = Zeny - .@sell;
			mes "[Christopher Guillenrow]";
			mes "Obrigado por comprar na minha oficina. Sinta-se à vontade para vir a qualquer momento, sempre que precisar, sempre que quiser.";
			close;
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Sinta-se à vontade para vir a qualquer momento, sempre que precisar. Adeus.";
			close;
		}
	case 4:
		mes "[Christopher Guillenrow]";
		mes "Eu posso purificar seu Oridecon e Elunium. Eu faço um minério refinado com 5 de cada um dos brutos. Bem... Qual você quer fazer?";
		next;
		switch(select("Fazer Oridecon:Fazer Elunium:Cancelar.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Christopher Guillenrow]";
				mes "Eu disse a você, preciso de 5 dos brutos de Oridecon para fazer um Oridecon.";
				close;
			}
			else {
				delitem 756,5;  // Pedra de Oridecon
				getitem 984,1; // Oridecon
				mes "[Christopher Guillenrow]";
				mes "Aqui está um Oridecon para você. Você será sempre bem-vindo aqui, estarei esperando por você.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Christopher Guillenrow]";
				mes "Eu disse a você, preciso de 5 dos brutos de Elunium para fazer um Elunium.";
				close;
			}
			else {
				delitem 757,5;  // Pedra de Elunium
				getitem 985,1; // Elunium
				mes "[Christopher Guillenrow]";
				mes "Arrr, aqui está seu Elunium. Seus negócios são sempre bem-vindos aqui, então sinta-se à vontade para voltar.";
				close;
			}
		case 3:
			mes "[Christopher Guillenrow]";
			mes "Sinta-se à vontade para vir a qualquer momento, sempre que precisar. Adeus.";
			close;
		}
	case 5:
		mes "[Christopher Guillenrow]";
		mes "Sinta-se à vontade para vir a qualquer momento, sempre que precisar e sempre que quiser. Adeus.";
		close;
	}
}

ein_in01,38,29,0	script	Paul Spanner	63,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Atualmente, você está carregando -";
		mes "- muitos itens consigo. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder um pouco de peso. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Bem-vindo, meu amigo.";
	mes "Na minha loja, você encontrará tudo o que precisa para forjar.";
	mes "Diga-me o que você precisa.";
	next;
	switch(select("Comprar Bigorna.:Comprar Itens de Forja.:Comprar Metal.:Processar Minérios.:Sair.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "A Bigorna é o item mais necessário para os Ferreiros.";
		mes "Como você usará uma Bigorna mais de uma vez, é melhor comprar uma boa.";
		next;
		switch(select("Bigorna - 30.000z.:Bigorna de Oridecon - 120.000z.:Bigorna Dourada - 300.000z.:Eu preciso de uma bigorna melhor.:Cancelar.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "Com esse dinheiro, você nem pode comprar uma bigorna de brinquedo!";
				close;
			}
			getitem 986,1; // Bigorna
			Zeny = Zeny-30000;
			mes "[Paul Spanner]";
			mes "É a bigorna mais barata, que tem a capacidade mais básica.";
			mes "Obrigado por usar minha loja. Se precisar de algo, é só me avisar.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "Com esse dinheiro, você nem pode comprar uma bigorna de brinquedo!";
				close;
			}
			getitem 987,1; // Bigorna de Oridecon
			Zeny = Zeny-120000;
			mes "[Paul Spanner]";
			mes "Ah, você tem olho para bigornas. Um Ferreiro precisa de uma bigorna pelo menos tão boa quanto esta.";
			mes "Obrigado por usar minha loja. Se precisar de algo, é só me avisar.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "Com esse dinheiro, você nem pode comprar uma bigorna de brinquedo!";
				close;
			}
			getitem 988,1; // Bigorna Dourada
			Zeny = Zeny-300000;
			mes "[Paul Spanner]";
			mes "Posso perceber sua ambição de se tornar um bom Ferreiro só de olhar para você escolhendo esta Bigorna Dourada!";
			mes "Esta bigorna certamente o ajudará a criar as melhores armas.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "Sinto muito, mas eu não vendo bigornas melhores do que a Bigorna Dourada.";
			mes "A menos que encontre a bigorna lendária de 'Linggell', não acho que conseguirá encontrar uma melhor do que a Bigorna Dourada em qualquer outro lugar.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "Se precisar de algo, é só me avisar.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "Você precisa de vários materiais para processar minérios e forjar armas.";
		mes "Eu tenho tudo o que você precisa. Dê uma olhada.";
		next;
		switch(select("Fornalha Portátil - 150z.:Martelo de Ferro - 1.000z.:Martelo Dourado - 3.000z.:Martelo de Oridecon - 5.000z.:Cancelar.")) {
		case 1:
			.@item = 612;
			.@item_cost = 150;
			.@item_weight = 200;
			mes "[Paul Spanner]";
			mes "Você definitivamente precisa desta fornalha para processar minérios!";
			next;
			break;
		case 2:
			.@item = 613;
			.@item_cost = 1000;
			.@item_weight = 200;
			break;
		case 3:
			.@item = 614;
			.@item_cost = 3000;
			.@item_weight = 300;
			break;
		case 4:
			.@item = 615;
			.@item_cost = 5000;
			.@item_weight = 400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "Se precisar de algo, é só me avisar.";
			close;
		}
		mes "[Paul Spanner]";
		mes "Então, quantos você precisa? Se quiser cancelar a negociação, digite '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "Você cancelou a negociação. Se precisar de algo, é só me avisar.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "Você só pode comprar 500 ou menos de uma vez.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "Você não tem dinheiro suficiente. Desculpe, eu não posso vendê-los com prejuízo.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Ei, você parece pálido. Que tal perder um pouco de peso primeiro.";
			close;
		}
		Zeny = Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Obrigado por usar minha loja. Se precisar de algo, é só me avisar.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "Eu tenho metal de alta qualidade.";
		mes "Então, qual metal você gostaria de comprar?";
		next;
		switch(select("Fracon - 200z.:Emveretarcon - 1.000z.:Sair.")) {
		case 1:
			.@item = 1010;
			.@item_price = 200;
			break;
		case 2:
			.@item = 1011;
			.@item_price = 1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "Se precisar de algo, é só me avisar.";
			close;
		}
		mes "[Paul Spanner]";
		mes "Então, quantos deles você precisa? Se quiser cancelar a negociação, digite '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "A negociação foi cancelada. Se precisar de algo, é só me avisar.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "Você só pode comprar 500 ou menos de uma vez.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "Você não tem dinheiro suficiente. Desculpe, eu não posso vendê-los com prejuízo.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Ei, você parece pálido. Que tal perder um pouco de peso primeiro?";
			close;
		}
		getitem .@item,.@input;
		Zeny = Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Obrigado por usar minha loja. Se precisar de algo, é só me avisar.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "Posso processar Oridecon e Elunium para você.";
		mes "Você precisa de 5 minérios para processá-los em um Oridecon ou Elunium.";
		mes "Então, qual você quer processar?";
		switch(select("Oridecon:Elunium:Sair.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "Você precisa de 5 minérios para processá-los em um puro Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Pedra de Oridecon
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "Aqui está. Obrigado por usar meu serviço.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "Você precisa de 5 minérios para processá-los em um puro Elunium.";
				close;
			}
			else {
				delitem 757,5; //Pedra de Elunium
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "Aqui está. Obrigado por usar meu serviço.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "Se precisar de algo, é só me avisar.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "Se precisar de algo, é só me avisar.";
		close;
	}
}

prt_in,63,60,0	script	Hollgrehenn	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Hollgrehenn]";
		mes "Eu sou um ferreiro que pode refinar armas e equipamentos.";
		mes "Você tem algum item que gostaria de refinar?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Hollgrehenn",0;
		end;
	}
}

morocc_in,73,38,6	script	Aragham	4W_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Aragham]";
		mes "Ferreiro do Deserto~";
		mes "Aragham Ssaleh~ que pode refinar qualquer coisa Ssaleh~";
		mes "Deixe-me ver o que você gostaria de refinar~";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Aragham",0;
		end;
	}
}

payon,144,173,5	script	Antonio	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Antonio]";
		mes "Pare de reclamar e me traga o equipamento que você deseja refinar.";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Antonio",0;
		end;
	}
}

alberta_in,28,58,0	script	Fredrik	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Fredrik Hermanthorn]";
		mes "Eu sou um ferreiro que pode refinar sua arma ou equipamento.";
		mes "Você tem algo que gostaria de refinar?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Fredrik",0;
		end;
	}
}

yuno_in01,171,21,4	script	Lambert	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Lambert]";
		mes "Bem...vindo... Este é... o melhor... ferreiro... em Juno.";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Lambert",0;
		end;
	}
}

ein_in01,24,87,5	script	Manthasman	4_M_DWARF,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Manthasman Pruhag]";
		mes "Hahaha, você já sabia. Então, gostaria de refinar algo?";
		mes "Vamos tentar algo arriscado hoje!";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Manthasman Pruhag",0;
		end;
	}
}

lhz_in02,282,20,7	script	Fulerr	4_M_LGTMAN,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Fulerr]";
		mes "Hehe... Você quer refinar?";
		mes "Hehe.. Considere feito..";
		mes "Hehe....";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Fulerr",0;
		end;
	}
}

//============================================================
//= Função Principal de Refino
//============================================================
//= Para permitir o refino automático seguro/múltiplo, ajuste o
//= segundo argumento para '1' na chamada da função.
//= Se você habilitar esta função, certifique-se de editar o valor de
//= .@safe para o máximo de refino seguro em refine.yml também.
//============================================================
function	script	refinemain	{
	disable_items;
	.@npc_name$ = getarg(0);
	.@features = getarg(1);
	mes "["+ .@npc_name$ +"]";
	mes "Eu sou o Ferreiro de Armas.";
	mes "Posso refinar todos os tipos de armas, armaduras e equipamentos, então me diga";
	mes "o que você quer que eu refine.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "Não acho que eu possa refinar nenhum item que você tenha...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //verificação personalizada
		mes "["+ .@npc_name$ +"]";
		mes "Você não está usando";
		mes "nada lá que";
		mes "eu possa refinar.";
		emotion ET_FRET;
		close;
	}
// Verificar se o item é refinável...
if(!getequipisenableref(.@part)) {
    mes "["+ .@npc_name$ +"]";
    mes "Eu não acho que eu possa";
    mes "refinar este item de jeito nenhum...";
    close;
}

// Verificar se o item já está +10
if(getequiprefinerycnt(.@part) >= 10) {
    mes "["+ .@npc_name$ +"]";
    mes "Não posso refinar isso";
    mes "mais. Este é o máximo";
    mes "de refinamento!";
    close;
}

.@refineitemid = getequipid(.@part); // salvar o ID do item
.@refinerycnt = getequiprefinerycnt(.@part); // salvar a contagem de refinamento
setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );

if( .@itemtype == IT_ARMOR ){
    .@equip_lv = getequiparmorlv( .@part );

    switch( .@equip_lv ){
        case 1:
            .@safe = 4;
            break;
        default:
            // TODO:
            close;
    }

    // Se o sistema VIP estiver habilitado, os preços para jogadores não VIP são consideravelmente mais altos.
    if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
        switch( .@equip_lv ){
            case 1:
                .@price = .@price * 10;
                break;
            default:
                // TODO:
                close;
        }
    }
}else if( .@itemtype == IT_WEAPON ){
    .@equip_lv = getequipweaponlv( .@part );

    switch( .@equip_lv ){
        case 1:
            .@safe = 7;
            break;
        case 2:
            .@safe = 6;
            break;
        case 3:
            .@safe = 5;
            break;
        case 4:
            .@safe = 4;
            break;
        default:
            // TODO:
            close;
    }

    // Se o sistema VIP estiver habilitado, os preços para jogadores não VIP são consideravelmente mais altos.
    if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
        switch( .@equip_lv ){
            case 1:
                .@price = .@price * 40;
                break;
            case 2:
                .@price = .@price * 50;
                break;
            case 3:
                .@price = .@price * 2;
                break;
            case 4:
                .@price = .@price * 2;
                break;
            default:
                // TODO:
                close;
        }
    }
}else{
    .@safe = 4;
}

// TODO: O que foi isso? Talvez sombra?
// case 5: .@price = .@price * 10; break;

if(.@features != 1) {
    mes "["+ .@npc_name$ +"]";
    mes "Para refinar isso, eu preciso";
    mes "de um ^003366"+getitemname(.@material)+"^000000 e";
    mes "uma taxa de serviço de " + .@price + " Zeny.";
    mes "Você realmente deseja continuar?";
    next;
    if(select("Sim:Não") == 2){
        mes "["+ .@npc_name$ +"]";
        mes "Sim...";
        mes "Não há necessidade de";
        mes "pressa. Leve o seu tempo.";
        close;
    }
    if(getequippercentrefinery(.@part) < 100) {
        mes "["+ .@npc_name$ +"]";
        mes "Oh não! Se eu continuar a";
        mes "refinar isso, há um risco de que poderia";
        switch(.@material) {
        case 985:
            mes "ser destruído! Isso significa que ^FF0000este equipamento^000000 e ^FF0000quaisquer cartas^000000 ou propriedades especiais adicionadas a essa armadura, ^FF0000serão perdidos^000000.";
            break;
        default:
            mes "ser destruído, e você ^FF0000perderia a arma^000000, quaisquer ^FF0000cartas na arma^000000,";
            mes "ou quaisquer propriedades especiais adicionadas.";
            break;
        }
        next;
        mes "["+getarg(0)+"]";
        mes "Não posso deixar mais claro.";
        mes "Uma vez que uma arma é destruída,";
        mes "não há como recuperá-la.";
        mes "Você realmente quer refinar?";
        next;
        if(select("Sim:Não") == 2){
            mes "["+ .@npc_name$ +"]";
            mes "Eu concordo completamente...";
            mes "Eu posso ser um grande refinador, mas às vezes até eu cometo erros.";
            close;
        }
    }
    if((countitem(.@material) < 1) || (Zeny < .@price)) {
        mes "["+ .@npc_name$ +"]";
        mes "Parece que você não tem";
        mes "Zeny suficiente ou "+getitemname(.@material)+"...";
        mes "Vá conseguir um pouco mais. Eu estarei";
        mes "aqui o dia todo se precisar de mim.";
        close;
    }
    Zeny = Zeny-.@price;
    delitem .@material,1;

    // anti-hack
    if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
        callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
        mes "["+ .@npc_name$ +"]";
        emotion ET_FRET;
        mes "Espere um segundo...";
        mes "Você acha que sou estúpido?!";
        mes "Você trocou o item enquanto eu não estava olhando! Saia daqui!";
        close;
    }

    if(getequippercentrefinery(.@part) <= rand(100)) {
        failedrefitem .@part;
        mes "["+ .@npc_name$ +"]";
        emotion (!rand(5))?ET_MONEY:ET_HUK;
        .@lose = rand(1,3);
        if (.@lose == 1) {
            mes "OH! MEU DEUS!";
            mes "Maldição! De novo não!";
            mes "Sinto muito, mas você sabe que a prática faz a perfeição.";
            mes "Um, certo? Heh heh...";
        } else if(.@lose == 2) {
            mes "Nãããããão!";
            mes "Quebrou!";
            mes "M-Me desculpe!";
        } else {
            mes "Caramba!";
            mes "Não aguentava";
            mes "muito mais tempero!";
            mes "Desculpe por isso...";
        }
        close;
    }
    mes "["+getarg(0)+"]";
    successrefitem .@part;
    emotion ET_SMILE;
    .@win = rand(1,3);
    if (.@win == 1) {
        mes "Perfeito!";
        mes "Heh heh!";
        mes "Mais uma vez,";
        mes "trabalho impecável";
        mes "do mestre~";
    } else if(.@win == 2) {
        mes "Sucesso...!";
        mes "Mais uma vez, meu incrível";
        mes "talento realmente deslumbra";
        mes "e brilha hoje.";
    } else {
        mes "Heh heh!";
        mes "Estou terminado.";
        mes "Sem dúvida, meu trabalho";
        mes "é do seu agrado.";
        mes "Pura, completa perfeição~";
    }
    close;
}

// Novas Funções de Refino ========================
if (.@refinerycnt < .@safe) {
    mes "["+ .@npc_name$ +"]";
    mes "Posso refinar isso até o limite seguro ou um número desejado de vezes. A escolha é sua.";
    next;
    .@menu2 = select("Até o limite seguro, por favor.","Eu decidirei quantas vezes.","Mudei de ideia...");
} else
    .@menu2 = 2;
switch(.@menu2){
case 1: 
    .@refinecnt = .@safe - .@refinerycnt;
    break;
case 2:
    next;
    mes "["+ .@npc_name$ +"]";
    mes "Quantas vezes você gostaria que eu refinasse seu item?";
    next;
    input .@refinecnt;
    .@refinecheck = .@refinecnt + .@refinerycnt;
    if (.@refinecnt < 1 || .@refinecheck > 10) {
        mes "["+ .@npc_name$ +"]";
        mes "Não posso refinar este item tantas vezes.";
        close;
    }
    if(.@refinecheck > .@safe) {
        .@refinecheck = .@refinecheck - .@safe;
        mes "["+ .@npc_name$ +"]";
        mes "Isso tentará refinar o equipamento " + .@refinecheck + " vezes além do limite seguro. Seu equipamento pode ser destruído... está bem?";
        next;
        if(select("Sim...","Não...") == 2){
            mes "["+ .@npc_name$ +"]";
            mes "Você disse isso... Que assim seja.";
            close;
        }
    }
    break;
case 3:
    next;
    mes "["+ .@npc_name$ +"]";
    mes "Você disse isso... Que assim seja.";
    close;
}

.@fullprice = .@price * .@refinecnt;
mes "["+ .@npc_name$ +"]";
mes "Isso custará " + .@refinecnt + " " + getitemname(.@material) + " e " + .@fullprice + " Zeny. Está bem?";
next;
if(select("Sim","Não...") == 2){
    mes "["+ .@npc_name$ +"]";
    mes "Você disse isso... Que assim seja.";
    close;
}
if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
    mes "["+ .@npc_name$ +"]";
    mes "Isso é tudo que você tem? Infelizmente, não posso trabalhar para você por um preço mais baixo. Tente se colocar no meu lugar.";
    close;
}
Zeny = Zeny - .@fullprice;
delitem .@material,.@refinecnt;
while(.@refinecnt){
    if (getequipisequiped(.@part) == 0) {
        mes "["+ .@npc_name$ +"]";
        mes "Olhe aqui... você não está usando nenhum item...";
        close;
    }
    // anti-hack
    if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
            callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || (.@menu2 == 1 && getequippercentrefinery(.@part) < 100)) {
        mes "["+ .@npc_name$ +"]";
        mes "Clang... Não, mas você imaginou que eu pudesse ser tão estúpido?!";
        mes "Você trocou isso...";
        mes "Saia antes que eu o atinja com meu martelo!!";
        close;
    } 
    mes "Clang, clang!!!";
    if(.@menu2 == 2 && getequippercentrefinery(.@part) <= rand(100)) {
        failedrefitem .@part;
        emotion ET_HUK;
        mes "["+ .@npc_name$ +"]";
        mes "WAHHHH!!! Sinto muito... Eu avisei que isso poderia acontecer...";
        .@refinecnt = .@refinecnt - 1;
        if(.@refinecnt == 0) close;
        mes "Aqui estão os Zenys e materiais não utilizados de volta...";
        getitem .@material,.@refinecnt;
        .@fullprice = .@refinecnt * .@price;
        Zeny = Zeny + .@fullprice;
        close;
    }
    successrefitem .@part;
    emotion ET_BEST;
    .@refinecnt = .@refinecnt - 1;
    .@refinerycnt = getequiprefinerycnt(.@part);
    next;
}
mes "["+ .@npc_name$ +"]";
mes "Tudo pronto... Volte em breve.";
close;
}

// Vendedores de Materiais
//============================================================
prt_in,56,68,5	script	Vurewell	86,{
	callfunc "phramain","Vurewell";
	end;
}
payon,145,178,3	script	Begnahd	88,{
	callfunc "phramain","Begnahd";
	end;
}
morocc_in,63,32,6	script	Sade	99,{
	callfunc "phramain","Sade";
	end;
}
alberta_in,13,71,3	script	Kahlamanlith	86,{
	callfunc "phramain","Kahlamanlith";
	end;
}
yuno_in01,171,27,4	script	Dilemma	88,{
	callfunc "phramain","Dilemma";
	end;
}
ein_in01,15,87,3	script	Tirehaus	86,{
	callfunc "phramain","Tirehaus";
	end;
}
lhz_in02,278,24,3	script	Krugg	86,{
	callfunc "phramain","Krugg";
	end;
}

// Funções dos Vendedores de Materiais
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens consigo. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "Eu vendo 2 tipos de metal";
	mes "para temperar armas.";
	mes "Eu tenho ^007777Fracon^000000 para armas de Nível 1";
	mes "e ^007777Emveretarcon^000000 para armas de Nível 2.";
	next;
	switch(select("Fracon - 200 Zenys:Emveretarcon - 1000 Zenys:Perguntar sobre outros Metais")) {
	case 1:
		.@material = 1010;
		.@price = 200;
		break;
	case 2:
		.@material = 1011;
		.@price = 1000;
		break;
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Outros metais?";
		mes "Bem, você precisará de metais especiais para melhorar armas de nível superior, ou qualquer tipo de armadura. Mas você sabe, Oridecon e Elunium são realmente";
		mes "difíceis de encontrar...";
		close;
	}
	mes "["+ .@npc_name$ +"]";
	mes "Então quantos você deseja comprar?";
	mes "Se você não quiser nenhum, por favor, digite o número '0'.";
	next;
	while(1) {
		input .@input;
		if (.@input == 0) {
			mes "["+ .@npc_name$ +"]";
			mes "O negócio foi";
			mes "cancelado.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "["+ .@npc_name$ +"]";
			mes "Tudo bem, você pode";
			mes "comprar até 500.";
			mes "Não mais que isso,";
			mes "entendeu? Bom.";
			next;
		}
		else {
			break;
		}
	}
	.@sell = .@input * .@price;
	if (Zeny < .@sell) {
		mes "["+ .@npc_name$ +"]";
		mes "Er...";
		mes "Você não tem";
		mes "dinheiro suficiente para comprar";
		mes ""+ .@input +" deles.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "["+ .@npc_name$ +"]";
		mes "Hmm...";
		mes "Eu não posso lhe dar nada se você não tiver espaço suficiente em seu inventário. Que tal colocar suas coisas extras no Armazém Kafra e tentar novamente?";
		close;
	}
	getitem .@material,.@input;
	Zeny = Zeny-.@sell;
	mes "["+ .@npc_name$ +"]";
	mes "Aqui está!";
	mes "Obrigado por";
	mes "sua preferência.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	Dietrich	84,{
	callfunc "orimain","Dietrich";
	end;
}
payon,137,178,5	script	Hakhim	88,{
	callfunc "orimain","Hakhim";
	end;
}
morocc_in,72,32,6	script	Abdula	99,{
	callfunc "orimain","Abdula";
	end;
}
alberta_in,21,63,5	script	Xenophon	84,{
	callfunc "orimain","Xenophon Zolotas";
	end;
}
yuno_in01,164,27,4	script	Delight	88,{
	callfunc "orimain","Delight";
	end;
}
ein_in01,18,82,6	script	Matestein	84,{
	callfunc "orimain","Matestein";
	end;
}
lhz_in02,281,24,5	script	Fruel	84,{
	callfunc "orimain","Fruel";
	end;
}

// Funções de Ori/Elu
//============================================================
function	script	orimain	{
	if (checkweight(1201, 1) == 0) {
		mes "- Espere um momento! -";
		mes "- Atualmente, você está carregando -";
		mes "- muitos itens. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder um pouco de peso. -";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "Eu posso purificar seus";
	mes "Rough Oridecons ou";
	mes "Rough Eluniums. Eu vou precisar";
	mes "de 5 Rough Stones para fazer";
	mes "1 puro para você.";
	next;
	switch(select("Criar Oridecon:Criar Elunium:Perguntar sobre Pedras Encantadas")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756, 5;  //Oridecon_Stone
			getitem 984, 1; // Oridecon
			mes "["+ .@npc_name$ +"]";
			mes "Aqui está o seu Oridecon.";
			mes "Você é bem-vindo a voltar";
			mes "sempre que quiser.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "Você está brincando comigo, certo?";
			mes "Eu acabei de dizer que preciso de 5 Rough Oridecons para fazer um Oridecon puro.";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757, 5;  //Elunium_Stone
			getitem 985, 1; // Elunium
			mes "["+ .@npc_name$ +"]";
			mes "Aqui está o seu Elunium.";
			mes "Você é bem-vindo a voltar";
			mes "sempre que quiser.";
			close;
		}
		else {
			mes "["+ .@npc_name$ +"]";
			mes "Você está brincando comigo, certo?";
			mes "Eu acabei de dizer que preciso de 5 Rough Eluniums para fazer um Elunium puro.";
			close;
		}
	case 3:
		mes "["+ .@npc_name$ +"]";
		mes "Pedras Encantadas...?";
		mes "Eu sou um ferreiro de pedras há 20 anos, então ouvi muito sobre elas. Supostamente, existem";
		mes "quatro tipos diferentes.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Cada Pedra Encantada possui uma das seguintes propriedades elementares: Terra, Vento, Água e Fogo.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "Se alguém combinar uma Pedra Encantada com uma arma durante a forja, essa arma terá a mesma propriedade que a Pedra.";
		next;
		mes "["+ .@npc_name$ +"]";
		mes "É claro que você precisa ter alguma habilidade de forja para produzir esse tipo de arma elemental.";
		close;
	}
}

// Reparadores de Equipamento
//============================================================
alberta_in,31,65,4	script	Reparador#alb	86,{
	callfunc "repairmain","Reparador";
	end;
}

moc_ruins,107,94,4	script	Reparador#moc	99,{
	callfunc "repairmain","Reparador";
	end;
}

payon,143,165,4	script	Reparador#pay	88,{
	callfunc "repairmain","Reparador";
	end;
}

prt_in,63,54,2	script	Reparador#prt	86,{
	callfunc "repairmain","Grendal";
	end;
}

yuno_in01,175,28,3	script	Reparador#juno	86,{
	callfunc "repairmain","Reparador";
	end;
}

geffen_in,34,166,3	script	Reparador#gef	99,{
	callfunc "repairmain","Reparador";
	end;
}

aldeba_in,38,60,3	script	Reparador#alde	86,{
	callfunc "repairmain","Reparador";
	end;
}

lhz_in02,284,14,3	script	Reparador#lhz	86,{
	callfunc "repairmain","Reparador";
	end;
}

prt_gld,139,117,4	script	Reparador#prt_gld	86,{
	callfunc "repairmain","Reparador";
	end;
}

gef_fild13,263,117,4	script	Reparador#gef_fild	86,{
	callfunc "repairmain","Reparador";
	end;
}

pay_gld,295,183,4	script	Reparador#pay_gld	86,{
	callfunc "repairmain","Reparador";
	end;
}

alde_gld,220,152,4	script	Reparador#alde_gld	86,{
	callfunc "repairmain","Reparador";
	end;
}

aru_gld,189,336,4	script	Reparador#aru_gld	86,{
	callfunc "repairmain","Reparador";
	end;
}

sch_gld,340,80,7	script	Reparador#sch_gld	86,{
	callfunc "repairmain","Reparador";
	end;
}

// Função de Reparo de Equipamento
//============================================================
function	script	repairmain	{
	.@repairprice = 5000;
	.@npc_name$ = getarg(0);
	mes "["+ .@npc_name$ +"]";
	mes "E aí!";
	mes "Você quer que eu";
	mes "repare algum item?";
	mes "Você pode contar comigo";
	mes "para reparos em itens!";
	next;
	switch(select("Na verdade, eu tenho alguns itens...:Nenhum no momento.")) {
	case 1:
		.@checkitem = 1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			.@checkitem = .@checkitem + 1;
		}
		.@checkitem = .@checkitem - 1;
		if (!.@checkitem) {
			mes "["+ .@npc_name$ +"]";
			mes "Nossa, isso é incrível!";
			mes "Você deve cuidar muito bem das suas coisas. Nenhum dos seus itens está danificado!";
			next;
			mes "["+ .@npc_name$ +"]";
			mes "Se todo mundo fosse como você, eu ficaria desempregado!! Haha~!";
			close;
		}
		mes "["+ .@npc_name$ +"]";
		mes "Hmm...";
		mes "Deixe-me ver...";
		mes "De todos os seus itens,";
		mes "" + .@checkitem + " estão danificados.";
		mes "Você gostaria de reparar?";
		next;
		.@totalcost = .@repairprice * .@checkitem;
		mes "["+ .@npc_name$ +"]";
		mes "Cada reparo custa " + .@repairprice + " Zeny. Então, para reparar todos os itens danificados, custaria " + .@totalcost + " Zeny! Você gostaria de reparar os itens?";
		next;
		switch(select("Sim:Não")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+ .@npc_name$ +"]";
				mes "Whoa whoa...";
				mes "Verifique sua carteira antes de receber a conta de reparo! Não posso consertar nada porque você não tem Zeny suficiente.";
				close;
			}
			.@checkitem2 = 1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				.@checkitem2 = .@checkitem2 + 1;
			}
			.@checkitem2 = .@checkitem2 - 1;
			if (.@checkitem == .@checkitem2) {
				Zeny = Zeny - .@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					.@checkitem = .@checkitem - 1;
				}
				mes "["+ .@npc_name$ +"]";
				mes "Okay! Tudo pronto. Agora, tente ser um pouco mais cuidadoso. Itens também têm vidas, você sabe.";
				close;
			}
			else {
				mes "["+ .@npc_name$ +"]";
				mes "Mmm? Algo está errado. Espere... Equipe os itens que você precisa reparar e depois volte até mim.";
				close;
			}
		case 2:
			mes "["+ .@npc_name$ +"]";
			mes "Bem, não é problema meu, mas não é bom deixar os itens danificados. Você deveria consertá-los o mais rápido possível!";
			close;
		}
	case 2:
		mes "["+ .@npc_name$ +"]";
		mes "Hohoho...";
		mes "Você não tem";
		mes "nenhum assunto comigo";
		mes "se não tiver nenhum";
		mes "item para reparar.";
		close;
	}
}

//============================================================
// Antigo histórico de alterações
//============================================================
//= 1.0 por Um monte de pessoas!
//=     Syrus22 - Refez completamente o script usando funções... também
//=     adicionou a opção de refino seguro automático e refino múltiplo.
//= 1.1 Corrigido bug de entrada negativa [Lupus]
//= 1.2 Adicionado reparadores adicionais em Morocc e Payon. Adicionado
//=     Christopher, o ferreiro, em Geffen. Editado alguns diálogos [kobra_k88]
//= 1.3 Novas localizações em Payon [Darkchild]
//=     Corrigida subtração de Zeny graças a jpnmania77 [kobra_k88]
//= 1.3a Temporariamente corrigido uma exploração. Precisa verificar as fontes
//=     para corrigir completamente o bug [Shinigami]
//=     Corrigidos preços de reparo [shadowlady]
//=     Corrigido bug que ignora requisitos graças a sir_loon [massdriller]
//=     Corrigido erro de itemid graças a -Vitamin- [massdriller]
//= 1.4 Verifica novamente o item no procedimento de refino para evitar
//=     hackers que podem alterar o item [dafide18]
//= 1.5 Corrigido travamentos devido ao uso inadequado de callfunc's [Skotlex]
//=     Lupus, não reverta essa correção importante novamente! >.<
//= 1.5a Corrigido um callfunc desnecessário, corrigiu o exploit anti-bot
//=     arruinando o loop de refino seguro [Skotlex]
//= 1.5b Corrigidos erros de ortografia [Nexon]
//= 1.6 Substituídos todos os breaks por ends conforme o novo motor de script [Skotlex]
//= 1.7 Adicionados refinadores de Einbroch (nomes personalizados ^^;) e uma loja duplicada de BS [Poki#3]
//= 1.8 Adicionados refinadores de Lighthalzen (nomes personalizados novamente ^^;) [Poki#3]
//= 1.8a Corrigido indicativo errado graças a NeoSaro [Lupus]
//= 1.9 Reescreveu o reparador, removeu o Aço do custo de reparo [DracoRPG]
//= 2.0 Corrigido check de presença de equipamento ausente. Thx2 Coltaro [Lupus]
//= 2.0a Adicionados checks de peso graças a Neouni [Playtester]
//= 2.0b Corrigidos os nomes de Lighthalzen e Einbroch refinadores graças a Maud_Dib [Kargha]
//= 2.1 Removidas duplicatas [Silent]
//= 2.2 Mudado o nome de "Emvertacon" para "Emveretarcon". [Samuray22]
//=     Graças a Barron-Monster.
//= 2.2b Mudado o nome de "Pharacon" para "Fracon". [Samuray22]
//=     Graças a Barron-Monster.
//= 2.3 Corrigidos nomes de NPC para se enquadrarem em restrições adequadas. [L0ne_W0lf]
//= 2.4 Atualizada a função de refinamento. Mais limpa e menos datada. [L0ne_W0lf]
//= 2.5 Atualização considerável para o refinador e comerciantes. :D [L0ne_W0lf]
//= 2.6 Corrigidos alguns bugs na criação de pedras puras. [L0ne_W0lf]
//= 2.7 A função de refinamento aceita parâmetro adicional. [L0ne_W0lf]
//=     0 = Sem recursos especiais; 1 = novos recursos de refino
//=     Atualizados reparadores e função. Não mostra mais o menu.
//= 2.7a Algumas melhorias na função do reparador. [L0ne_W0lf]
//= 2.8 Alterada a variável inexistente .@matname$ para getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Adicionado fornecedor adequado para ferreiros em Einroch. [L0ne_W0lf]
//=     Atualizado comentário de recursos desatualizados para refletir o novo uso.
//= 2.8a Pequeno bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Movido reparador de Morocc para Morocc Ruins. [L0ne_W0lf]
//============================================================

