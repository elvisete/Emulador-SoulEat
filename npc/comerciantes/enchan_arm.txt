//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

prontera,165,60,2	script	Aprendiz Artesão	73,{
	disable_items;
	mes "[Aprendiz Artesão]";
	if (Zeny >= 400000) {
		mes "Tenho estudado maneiras de aprimorar uma armadura para maximizar sua capacidade.";
		next;
		mes "[Aprendiz Artesão]";
		mes "Encantamento é uma habilidade incrível que infunde poderes misteriosos em um soquete oculto da armadura.";
		next;
		mes "[Aprendiz Artesão]";
		mes "No entanto, você deve ter em mente que se houver duas armaduras do mesmo tipo em sua posse, o encantamento será aplicado na ordem em que estão em seu inventário.";
		next;
		mes "[Aprendiz Artesão]";
		mes "Nesse caso, o encantamento pode ser aplicado a um item que você não pretendia encantar. Portanto, traga ^5555ffUMA Armadura^000000 que você deseja encantar para garantir...";
		next;
		mes "[Aprendiz Artesão]";
		mes "Não me responsabilizo pelo que pode acontecer se você tiver mais de uma do mesmo tipo em seu inventário.";
		next;
		switch(select("Armadura não Entalhada.:Armadura Entalhada.:Armadura de Alta Qualidade.:Quem sabe na próxima vez.")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[Aprendiz Artesão]";
			mes "Por favor, volte quando tiver interesse em encantar sua armadura.";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "Eu sou responsável por encantar armaduras. Em poucas palavras, tenho estudado maneiras de fortalecer armaduras.";
	next;
	mes "[Aprendiz Artesão]";
	mes "Se por acaso você quiser encantar sua armadura, traga-me 400,000 zeny e a armadura que deseja encantar, e estará pronto para prosseguir.";
	close;

S_EnchantArmor:
	set .@itemid, getarg(0);
	set .@failrate, getarg(1);
	mes "[Aprendiz Artesão]";
	if (countitem(.@itemid) == 1) {
		mes "O encantamento de soquete custará 400,000 zeny. E haverá uma opção aleatória encantada. Claro, há uma chance de quebrar sua armadura.";
		next;
		mes "[Aprendiz Artesão]";
		mes "Primeiro e mais importante.";
		mes "^ff5555O Nível de Refino Existente da Armadura";
		mes "e Cartas serão PERDIDOS.^000000";
		mes "Ainda deseja tentar o encantamento?";
		next;
		if (select("Hmm... Deixe-me pensar sobre isso.:Vá em frente.") == 1) {
			mes "[Aprendiz Artesão]";
			mes "Bem, não posso culpá-lo. A segurança em primeiro lugar, não é mesmo?";
			mes "Tenha um bom dia.";
			close;
		}
		mes "[Aprendiz Artesão]";
		mes "Bem aventureiro, não é? Então, vamos lá?";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[Aprendiz Artesão]";
			mes "Desculpe, mas você não tem dinheiro suficiente.";
			close;
		}
		progressbar "ffff00",7;
		set Zeny, Zeny - 400000;
		delitem .@itemid, 1;
		switch (rand(1, .@failrate)) {
			case 1: set .@addpart, 4702; break;
			case 2: set .@addpart, 4712; break;
			case 3: set .@addpart, 4722; break;
			case 4: set .@addpart, 4732; break;
			case 5: set .@addpart, 4742; break;
			case 6: set .@addpart, 4752; break;
			case 7:
			case 8: set .@addpart, 4701; break;
			case 9:
			case 10: set .@addpart, 4711; break;
			case 11:
			case 12: set .@addpart, 4721; break;
			case 13:
			case 14: set .@addpart, 4731; break;
			case 15:
			case 16: set .@addpart, 4741; break;
			case 17:
			case 18: set .@addpart, 4751; break;
			case 19:
			case 20:
			case 21: set .@addpart, 4700; break;
			case 22:
			case 23:
			case 24: set .@addpart, 4710; break;
			case 25:
			case 26:
			case 27: set .@addpart, 4720; break;
			case 28:
			case 29:
			case 30: set .@addpart, 4730; break;
			case 31:
			case 32:
			case 33: set .@addpart, 4740; break;
			case 34:
			case 35:
			case 36: set .@addpart, 4750; break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[Aprendiz Artesão]";
				mes "Bem, isso é uma pena.";
				mes "O equipamento solicitado falhou no encantamento.";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "Hmm? Não há nada para ser encantado!";
		mes "Por favor, volte com APENAS UM equipamento para ser encantado.";
		close;
	}
}
