//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

-	script	Leablem#dummy::SocketEnchant2	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- itens demais. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Leablem]";
	mes "Bom dia. Meu nome é Leablem,";
	mes "e eu sou um técnico especializado em";
	mes "adicionar slots a armas e armaduras.";
	mes "Às vezes pode ser muito difícil, mas eu gosto";
	mes "do que faço e me orgulho disso.";
	next;
	mes "[Leablem]";
	mes "Aprendi a maioria das coisas com meu irmão, Seyablem,";
	mes "mas acho que ainda tenho muito a aprender.";
	mes "Então, por favor, entenda que meus serviços de inserção de slots são limitados,";
	mes "ao contrário do meu irmão Seyablem.";
	next;
	mes "[Leablem]";
	mes "Meu irmão não ficou tão feliz em me ensinar";
	mes "suas habilidades porque estava preocupado que suas habilidades";
	mes "não seriam mais únicas. No entanto, ele fez o melhor";
	mes "para me ensinar.";
	next;
	mes "[Leablem]";
	mes "Graças a ele, aprendi muitas coisas.";
	mes "Não tenho muita confiança";
	mes "em tentar muitas coisas diferentes,";
	mes "mas farei o que";
	mes "puder fazer por enquanto.";
	next;
	mes "[Leablem]";
	mes "Na verdade, há uma coisa";
	mes "que meu irmão não quer que eu faça.";
	mes "Não quero deixá-lo chateado, então... vamos pular essa parte.";
	mes "Não direi mais nada sobre isso!";
	next;
	mes "[Leablem]";
	mes "De qualquer forma, você tem algum equipamento";
	mes "ao qual gostaria de adicionar slots?";
	mes "Minha taxa de serviço, os materiais,";
	mes "e a chance de sucesso dependem de";
	mes "o item específico com o qual estou trabalhando.";
	next;
	switch(select("Arma:Armadura:Sobre aquela coisa..:Mais informações:Sair"))
	{
		case 1:
			mes "[Leablem]";
			mes "Arma, você disse? Claro, sem problemas.";
			mes "As armas são divididas em 4 classes diferentes";
			mes "dependendo da dificuldade do trabalho.";
			mes "A classe C é a mais fácil, e a classe S é a mais difícil.";
			mes "Qual classe você gostaria de tentar?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "Classe C, entendi.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Livro da Mãe Terra:Livro das Ondas:Livro do Golpe de Vento:Livro do Sol Ardente"))
					{
						case 1: callfunc "Func_Socket2",1554,1569,40,66,200,1010,10;
						case 2: callfunc "Func_Socket2",1553,1568,40,66,200,1010,10;
						case 3: callfunc "Func_Socket2",1556,1571,40,66,200,1010,10;
						case 4: callfunc "Func_Socket2",1555,1570,40,66,200,1010,10;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a classe B média.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Machado dos Orc:Cimitarra:Clava com Espetos"))
					{
						case 1: callfunc "Func_Socket2",1304,1309,40,66,200,984,1;
						case 2: callfunc "Func_Socket2",1113,1114,40,66,200,984,1;
						case 3: callfunc "Func_Socket2",1523,1538,40,61,300,984,1;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A? Parece que você quer um pouco demais. Mas, sem problemas.";
					mes "Então, que tipo de arma você tem?";
					next;
					switch(select("Matadora de Dragão:Katar da Espinheira Empoeirada:Katar da Chama Impetuosa:Katar Gélida:Katar do Vento Perfurante:Maça de Ouro:Alaúde Oriental:Chicote da Rainha:Lança Espectral:Gae Bolg:Schweizersabel"))
					{
						case 1:  callfunc "Func_Socket2",13001,13030,40,61,500,984,2;
						case 2:  callfunc "Func_Socket2", 1257, 1276,40,66,500,984,2;
						case 3:  callfunc "Func_Socket2", 1258, 1277,40,66,500,984,2;
						case 4:  callfunc "Func_Socket2", 1256, 1275,40,66,500,984,2;
						case 5:  callfunc "Func_Socket2", 1259, 1278,40,66,500,984,2;
						case 6:  callfunc "Func_Socket2", 1524, 1539,40,61,500,984,2;
						case 7:  callfunc "Func_Socket2", 1918, 1922,40,61,500,984,2;
						case 8:  callfunc "Func_Socket2", 1970, 1976,40,61,500,984,2;
						case 9:  callfunc "Func_Socket2", 1477, 1479,40,56,500,984,2;
						case 10: callfunc "Func_Socket2", 1474, 1480,40,56,500,984,2;
						case 11: callfunc "Func_Socket2", 1167, 1178,40,61,500,984,2;
					}
				case 4:
					mes "[Leablem]";
					mes "Hein, Classe S?! Oh meu Deus, você está falando sério? *Tremble*";
					mes "Então... qual arma da Classe S você gostaria de adicionar um slot?!";
					next;
					switch(select("Zéfiro:Destruidor de Malha:Caçadora de Dragões:Destruidor de Espada:Adaga do Algoz:Grande Cruz:Executadora"))
					{
						case 1: callfunc "Func_Socket2",1468, 1481,40,51,1000,984,5,999,10;
						case 2: callfunc "Func_Socket2",1225,13032,40,51,1000,984,5,999,10;
						case 3: callfunc "Func_Socket2",1166, 1180,40,51,1000,984,5,999,10;
						case 4: callfunc "Func_Socket2",1224,13031,40,51,1000,984,5,999,10;
						case 5: callfunc "Func_Socket2",1232,13033,40,51,1000,984,5,999,10;
						case 6: callfunc "Func_Socket2",1528, 1540,40,51,1000,984,5,999,10;
						case 7: callfunc "Func_Socket2",1169, 1179,40,51,1000,984,5,999,10;
					}
			}
		case 2:
			mes "[Leablem]";
			mes "Armadura, você disse? Claro, sem problemas.";
			mes "As armaduras são divididas em 4 classes diferentes,";
			mes "dependendo da dificuldade do trabalho.";
			mes "A classe C é a mais fácil, e a classe S é a mais difícil.";
			mes "Qual classe você gostaria de tentar?";
			next;
			switch(select("C:B:A:S"))
			{
				case 1:
					mes "[Leablem]";
					mes "Classe C, entendi.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Girassol:Capelo:Laço Grande:Quepe"))
					{
						case 1: callfunc "Func_Socket2",2253,5351,40,66,100,999,3;
						case 2: callfunc "Func_Socket2",5012,5347,40,66,100,999,3;
						case 3: callfunc "Func_Socket2",2244,5348,40,66,100,999,5;
						case 4: callfunc "Func_Socket2",5016,5349,40,66,100,999,5;
					}
				case 2:
					mes "[Leablem]";
					mes "Você escolheu a classe média B.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Anel de Caveira:Salto Alto"))
					{
						case 1: callfunc "Func_Socket2",2609,2715,40,61,300,999,5;
						case 2: callfunc "Func_Socket2",2409,2432,40,61,300,999,5;
					}
				case 3:
					mes "[Leablem]";
					mes "Classe A? Parece que você quer algo mais. Mas sem problemas.";
					mes "Então, que tipo de armadura você tem?";
					next;
					switch(select("Badana Pirata:Botas Pretas de Couro"))
					{
						case 1: callfunc "Func_Socket2",2287,5350,50,61,500,985,1; // 50,61 - it's not a typo
						case 2: callfunc "Func_Socket2",2425,2434,40,51,500,985,1;
						//case 3: callfunc "Func_Socket2",15053,15056,x,x,x,x,x;	// Special Ninja Suit (Needs more info)
					}
				case 4:
					mes "[Leablem]";
					mes "Hã, Classe S?! Meu Deus, você está falando sério? *Tremendo*";
					mes "Então... qual armadura Classe S você gostaria de adicionar um slot?";
					next;
					switch(select("Vestimenta Arcana:Batina:Spiritus Sancti:Camiseta:Roupa de Baixo"))
					{
						case 1: callfunc "Func_Socket2",2334,2372,40,51,1000,985,1; // Mage Coat has 10% success chance
						case 2: callfunc "Func_Socket2",2327,2373,45,51,1000,985,1; // the rest has 5%
						case 3: callfunc "Func_Socket2",2111,2128,45,51,1000,985,1;
						case 4: callfunc "Func_Socket2",2522,2523,45,51,1000,985,1;
						case 5: callfunc "Func_Socket2",2339,2371,45,51,1000,985,1;
					}
			}
		case 3:
			mes "[Leablem]";
			mes "Oh, não é nada especial, mas ele acha";
			mes "que não é algo para os humanos mexerem.";
			mes "Eu não quero chatear meu irmão,";
			mes "então é melhor eu esquecer isso.";
			next;
			mes "[Leablem]";
			mes "... Esse serviço em si tem uma chance de sucesso muito alta,";
			mes "mas requer muitos materiais e uma taxa de serviço elevada.";
			mes "Duvido muito que alguém realmente queira usar";
			mes "o serviço, mesmo que eu o abra.";
			next;
			mes "[Leablem]";
			mes "... Ummm...";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes ".............";
			next;
			mes "[Leablem]";
			mes "Você parece interessado em usar esse serviço.";
			mes "Se quiser, posso pelo menos te contar sobre isso.";
			mes "Mas você deve prometer manter isso em segredo.";
			mes "Se meu irmão descobrir o que vou te contar,";
			mes "ele vai me dar uma surra!";
			next;
			mes "[Leablem]";
			mes "Esse serviço especial tem uma chance de sucesso de 90%,";
			mes "mas requer 2 Gold e 200 milhões de zeny:";
			mes "Eu posso adicionar slots ao Hat of the Sun God.";
			next;
			mes "[Leablem]";
			mes "Meu irmão disse que, desde os tempos antigos, é proibido modificar esse chapéu.";
			mes "Mas eu acredito que os humanos são muito curiosos para";
			mes "não experimentar, especialmente quando é proibido.";
			mes "Só não quero que meu irmão grite comigo. Isso é tudo.";
			next;
			switch(select("Usar o Serviço","Sair"))
			{
				case 1:
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
										mes "...O quê... O quê? Não!";
					mes "Me olhar desse jeito sujo";
					mes "não vai me fazer fazer isso!";
					mes "Não, eu disse não! Não é não! Não!";
					next;
					mes "[Leablem]";
					mes "Não, eu não vou! Não!";
					next;
					mes "[Leablem]";
					mes "Eu não posso fazer isso...";
					next;
					mes "[Leablem]";
					mes "Eu não posso fazer... Isso...";
					next;
					mes "[Leablem]";
					mes ".................";
					next;
					mes "[Leablem]";
					mes "Umm...";
					next;
					mes "[Leablem]";
					mes "Você é tão persistente. Ok, vou abrir uma exceção para você.";
					mes "Só não conte para ninguém,";
					mes "especialmente para o meu irmão. Você promete?";
					next;
					mes "[Leablem]";
					mes "Ok, então, por favor, me traga";
					mes "^FF00002 Ouro, 200 milhões de zeny -- a taxa de serviço --,";
					mes "^FF0000e um Hat of the Sun God.";
					mes "^FF0000Você sabe que sua chance de sucesso é de 90% com isso, não é?^000000";
					mes "Não temos tempo a perder,";
					mes "então vamos aos negócios imediatamente.";
					next;
					if ((Zeny > 199999999) && (countitem(969) > 1) && (countitem(5022) > 0))
					{
						mes "[Leablem]";
						mes "Vamos começar agora?";
						mes "Reze para seus deuses por boa sorte.";
						next;
						set .@a, rand(1,100);
						if ((.@a > 4) && (.@a < 95))
						{
							specialeffect EF_LORD;
							mes "[Leablem]";
							mes "Excelente! Wow, eu acho que a chance de sucesso de 90% é verdadeira!";
							mes "Veja, o slot foi adicionado no lugar perfeito.";
							mes "Parabéns.";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							getitem 5353,1; //Helm_Of_Sun_
							next;
							mes "[Leablem]";
							mes "Esta é a primeira e última vez que faço isso por você.";
							mes "Nunca conte a ninguém";
							mes "sobre isso. Certo?";
							close;
						}
						else
						{
							specialeffect EF_SUI_EXPLOSION;
							mes "[Leablem]";
							mes "Wah! Oh meu deus, falhamos!";
							mes "Meu deus, acho que tivemos o pior tipo de sorte...";
							mes "O que devemos fazer? ...É por isso que";
							mes "eu não queria fazer isso em primeiro lugar!";
							delitem 5022,1; //Helm_Of_Sun
							delitem 969,2; //Gold
							set Zeny, Zeny - 200000000;
							next;
							mes "[Leablem]";
							mes "Não, nunca me peça para fazer uma coisa tão arriscada novamente!";
							mes "Bah~";
							close;
						}
					}
					else
					{
						mes "[Leablem]";
						mes "Umm...";
						next;
						mes "[Leablem]";
						mes "Você não tem materiais suficientes.";
						mes "Cara, por que você até pediu para eu tentar?";
						mes "Não vou fazer isso de novo para você,";
						mes "mesmo se trouxer materiais suficientes. Bah~";
						close;
					}
				case 2:
					mes "[Leablem]";
					mes "Nunca conte a ninguém";
					mes "sobre o que eu te contei. Certo?";
					close;
			}
		case 4:
			mes "[Leablem]";
			mes "Você quer saber mais informações?";
			mes "Bem, eu não acho que há algo";
			mes "que você precisa saber especificamente.";
			mes "Mas posso ao menos te dar uma dica.";
			next;
			mes "[Leablem]";
			mes "Os técnicos de slot em cada cidade";
			mes "oferecem diferentes chances de sucesso de encaixe";
			mes "dependendo do nível de aprimoramento do seu equipamento.";
			mes "Eu não sei se isso é verdade,";
			mes "mas posso ver a diferença em cada cidade.";
			next;
			mes "[Leablem]";
			mes "Por exemplo, um técnico de slot em uma cidade";
			mes "oferece uma maior chance de sucesso para";
			mes "itens de equipamento em níveis de aprimoramento mais altos,";
			mes "enquanto um técnico em outra cidade tem mais sucesso";
			mes "em níveis de aprimoramento mais baixos.";
			next;
			mes "[Leablem]";
			mes "Quando você falha em adicionar um slot, o equipamento";
			mes "ficará quebrado e desaparecerá.";
			mes "Eu faço o meu melhor para ter sucesso, mas";
			mes "o encaixe é um trabalho complicado.";
			next;
			mes "[Leablem]";
			mes "Ah, há uma coisa importante com a qual você precisa ter cuidado.";
			mes "^FF0000Tente não carregar dois itens de equipamento idênticos";
			mes "^FF0000quando adicionar slots.";
			mes "^FF0000Caso contrário, slots podem ser adicionados a um dos itens escolhidos aleatoriamente,";
			mes "^FF0000e no pior caso, você pode perder um item que é mais valioso para você.^000000";
			next;
			mes "[Leablem]";
			mes "Espero que essas informações sejam úteis para você.";
			mes "Obrigado.";
			close;
		case 5:
			mes "[Leablem]";
			mes "Tudo bem, então.";
			close;
	}
}

payon,236,199,3	duplicate(SocketEnchant2)	Leablem#pay	86
lighthalzen,96,137,3	duplicate(SocketEnchant2)	Leablem#lhz	86
prt_in,22,60,6	duplicate(SocketEnchant2)	Leablem#prt	86

function	script	Func_Socket2	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Leablem]";
	if (.@item_req[1] != 0 && .@req_amount[1] != 0)
	{
		mes getitemname(.@item_id) +"... Tudo bem, você precisa me trazer";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req /1000 +",000") : .@zeny_req) +",000 zeny como taxa de serviço,";
		mes "^FF0000"+ .@req_amount[0] +" un. de "+ getitemname(.@item_req[0]) +" e "+ .@req_amount[1] +" un. de "+ getitemname(.@item_req[1]) +".";
		mes "^FF0000É claro, você precisa do item "+ getitemname(.@item_id) +".^000000";
	}
	else
	{
		mes getitemname(.@item_id) +"? Tudo bem, então você precisa me trazer";
		mes "^FF0000"+ (.@zeny_req >= 1000 ? (.@zeny_req/1000 +",000") : .@zeny_req) +",000 zeny como taxa de serviço e "+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +" como requisito.";
		mes "^FF0000E, claro, não se esqueça de me trazer um(a) "+ getitemname(.@item_id) +".";
		mes "^FF0000Você deve ter todos os itens.^000000";
	}
	next;
	mes "[Leablem]";
	mes "Você já trouxe todos eles?";
	mes "Para sua informação, se falhar na criação do slot,";
	mes "você perderá todos os itens necessários, assim como o "+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "arma" : "armadura") +".";
	mes "Lembre-se também, se o "+ ((getiteminfo(.@item_id, ITEMINFO_LOCATIONS)&EQP_HAND_R) ? "arma" : "armadura") +" foi aprimorado e recebeu um card,";
	mes "você perderá tudo, mesmo se tiver sucesso na criação do slot.";
	next;
	switch(select("Pedir pela criação do slot.:Tentar na próxima vez."))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// se não houver .@req_amount[0] e .@req_amount[1] fornecidos, verifique a contagem de itens contra 0 (ainda precisamos de um ID de item válido)
			{
				mes "[Leablem]";
				mes "Tudo bem então, vamos começar o trabalho!";
				mes "É melhor rezar por um resultado bem-sucedido.";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect EF_LORD;
					mes "[Leablem]";
					mes "Ótimo, parece que foi bem-sucedido.";
					mes "Parece muito bem feito. Parabéns!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Leablem]";
					mes "Até mais, amigo!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Leablem]";
					mes "Wah! ... Me desculpe, falhou.";
					mes "No entanto, eu sou completamente inocente.";
					mes "Isso é sorte sua, e é destinado por Deus, certo?";
					mes "Não fique tão desapontado,";
					mes "e tente na próxima vez.";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Leablem]";
					mes "Até mais, amigo!";
					close;
				}
			}
			else
			{
				mes "[Leablem]";
				mes "Você é burro ou o quê? Você não trouxe todos os itens necessários!";
				mes "Vá buscá-los rapidinho!";
				close;
			}
		case 2:
			mes "[Leablem]";
			mes "Até a próxima.";
			close;
	}
}
