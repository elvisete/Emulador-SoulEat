//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

que_ng,72,29,3	script	Mercador Kunai Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "Eu sou Kashin, distribuidor";
		mes "de Kunai para Ninjas. Dê";
		mes "uma olhada ao redor e me";
		mes "avise se estiver interessado";
		mes "em alguma das minhas mercadorias.";
		next;
		switch(select("10 Kunai de Veneno Caído:10 Kunai de Gelo:10 Kunai de Vento Forte:10 Kunai de Terra Negra:10 Kunai de Onda de Calor:Cancelar")) {
		case 1: callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2: callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3: callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4: callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5: callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "Bem, então, obrigado";
			mes "por visitar minha loja.";
			mes "Por favor, volte quando";
			mes "precisar comprar algumas";
			mes "Kunais. Adeus por agora~";
			close;
		}
	}
	mes "[Kashin]";
	mes "Eu sou Kashin, distribuidor";
	mes "de Kunai para Ninjas. Se você";
	mes "tiver amigos que são";
	mes "Ninjas, você pode querer";
	mes "falar sobre mim para eles.";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "Você pode trocar";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" e";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" por cada";
	mes "conjunto de 10 "+ getitemname(.@item_id) +".";
	next;
	mes "[Kashin]";
	mes "Posso dar a você no máximo";
	mes "500 conjuntos de Kunais de cada vez.";
	mes "Se você quiser cancelar a troca, então, por favor, digite ''0.'' Quantos";
	mes "conjuntos de Kunais você gostaria?";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "Eh? Desculpe, mas";
		mes "não posso te dar tantos";
		mes "conjuntos de Kunai. Por favor,";
		mes "digite um valor menor que 500.";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "Hmm, você não tem";
		mes "itens suficientes para esta";
		mes "troca de Kunai. Por favor,";
		mes "verifique seus itens novamente.";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "Hmm, parece que o seu";
		mes "Inventário não tem espaço";
		mes "suficiente para armazenar";
		mes "mais itens. Melhor liberar";
		mes "algum espaço primeiro.";
		close;
	}
	mes "[Kashin]";
	mes "Ótimo, tudo está em";
	mes "ordem, então vamos";
	mes "completar esta troca.";
	mes "Tenho certeza de que você";
	mes "ficará satisfeito com";
	mes "essas Kunais.";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}
