//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

payon_in01,5,134,5	script	Inventor Jaax	89,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "[Inventor Jaax]";
		mes "Ei, você está carregando";
		mes "muitas coisas. Que tal guardar tudo na Kafra Storage? Podemos conversar depois que fizer isso, certo?";
		close;
	}
	mes "[Inventor Jaax]";
	mes "Meu nome é Jaxx.";
	mes "Sem ego, posso";
	mes "dizer que sou talvez o";
	mes "^663300maior inventor de nosso tempo^000000.";
	next;
	mes "[Inventor Jaxx]";
	mes "Desta vez, eu";
	mes "criei algo";
	mes "verdadeiramente extraordinário. Eu os chamo de... ^663300Magic Quivers^000000 !! Isso será lembrado na história como uma";
	mes "revolução de flechas!";
	emotion ET_BEST;
	next;
	mes "[Inventor Jaax]";
	mes "Estudei magia e aljavas por anos, trabalhando noite e dia até finalmente descobrir como condensar flechas com magia! Com aljavas mágicas, você carregará mais flechas, mas com menos peso!";
	next;
	mes "[Inventor Jaax]";
	mes "Gostaria de experimentar o uso de uma das minhas aljavas de flechas? Não tenho dúvidas de que alguém como você pode apreciar meu gênio!";
	next;
	switch(select("Aljava:Aljava de Flecha de Ferro:Aljava de Flecha de Aço:Aljava de Flecha de Oridecon:Aljava de Flecha de Fogo:Aljava de Flecha de Prata:Aljava de Flecha de Vento:Aljava de Flecha de Pedra:Aljava de Flecha de Cristal:Aljava de Flecha Sombria:Aljava de Flecha Imaterial:Aljava de Flecha Enferrujada")) {
	case 1: callsub S_BuyQuiver,1750,500,500,12004;
	case 2: callsub S_BuyQuiver,1770,500,500,12005;
	case 3: callsub S_BuyQuiver,1753,500,500,12006;
	case 4: callsub S_BuyQuiver,1765,500,500,12007;
	case 5: callsub S_BuyQuiver,1752,500,500,12008;
	case 6: callsub S_BuyQuiver,1751,500,500,12009;
	case 7: callsub S_BuyQuiver,1755,500,500,12010;
	case 8: callsub S_BuyQuiver,1756,500,500,12011;
	case 9: callsub S_BuyQuiver,1754,500,500,12012;
	case 10: callsub S_BuyQuiver,1767,500,500,12013;
	case 11: callsub S_BuyQuiver,1757,500,500,12014;
	case 12: callsub S_BuyQuiver,1762,500,500,12015;
	}
	//Isso não parece ser acessível...
	//mes "[Inventor Jaax]";
	//mes "Há algo que";
	//mes "você quer?";
	//close;

S_BuyQuiver:
	.@item_id = getarg(0);
	.@amount = getarg(1);
	.@zeny_req = getarg(2);

	if (countitem(.@item_id) >= .@amount) {
		mes "[Inventor Jaax]";
		mes "Excelente!";
		mes "Você está carregando algumas flechas? Vou lhe fornecer uma aljava que pode carregar "+ .@amount +" de suas flechas "+ getitemname(.@item_id) +" por apenas ^FF3131"+ .@zeny_req +" Zeny^000000.";
		next;
		switch(select("Armazenar o máximo possível de flechas na aljava:Comprar 1 aljava:Cancelar")) {
		case 1:
			.@arrows = countitem(.@item_id);
			set .@quiver,.@arrows / .@amount;
			set .@arrows_used,.@quiver * .@amount;
			.@arrow_zeny01 = .@quiver * .@zeny_req;
			mes "Número de";
			mes "Flechas: ^3131FF"+.@arrows+" ^000000";
			mes "Número Máximo";
			mes "de Aljavas";
			mes "Compráveis: ^3131FF"+.@quiver+" ^000000";
			mes "Zeny necessário: ^3131FF"+.@arrow_zeny01+" Zeny^000000";
			next;
			mes "[Inventor Jaax]";
			mes "Gostaria de";
			mes "comprar o máximo de";
			mes "aljavas possíveis para as flechas";
			mes "que você está carregando atualmente?";
			next;
			if (select("Sim:Cancelar") == 1) {
				if (.@arrow_zeny01 < Zeny) {
					mes "[Inventor Jaax]";
					mes "Aqui está!";
					mes "Apenas lembre-se, ^FF0000você não poderá usar a aljava quando o peso que você carrega atingir 90% do limite máximo de peso^000000.";
					set Zeny, Zeny-.@arrow_zeny01;
					delitem .@item_id,.@arrows_used;// Flecha
					getitem getarg(3),.@quiver; //Aljava
					next;
					mes "[Inventor Jaax]";
					mes "Então...";
					mes "Apenas acompanhe";
					mes "quanto você está";
					mes "carregando de vez";
					mes "em quando e você deverá";
					mes "ficar bem.";
					close;
				}
				else {
					mes "[Inventor Jaax]";
					mes "Desculpe, mas você não tem dinheiro suficiente. Eu não posso simplesmente dar isso depois de trabalhar anos";
					mes "para desenvolver essa tecnologia revolucionária!";
					close;
				}
			}
			mes "[Inventor Jaax]";
			mes "Você mudou de ideia?";
			mes "Quando a glória de possuir";
			mes "uma aljava está tão próxima?";
			close;
		case 2:
			if (Zeny > .@zeny_req) {
				mes "[Inventor Jaax]";
				mes "Aqui está!";
				mes "Apenas lembre-se, ^FF0000você não poderá usar a aljava quando o peso que você carrega atingir 90% do limite máximo de peso^000000.";
				Zeny = Zeny -.@zeny_req;
				delitem .@item_id,.@amount;// Flecha
				getitem getarg(3),1; //Aljava
				next;
				mes "[Inventor Jaax]";
				mes "Então...";
				mes "Apenas acompanhe";
				mes "quanto você está";
				mes "carregando de vez";
				mes "em quando e você deverá";
				mes "ficar bem.";
				close;
			}
			else {
				mes "[Inventor Jaax]";
				mes "Você nem mesmo";
				mes "tem "+ .@zeny_req +" Zeny?";
				mes "Sinto muito. Eu não tinha";
				mes "ideia de que você estava tão...";
				mes "^333333Desprovido^000000.";
				close;
			}
		case 3:
			mes "[Inventor Jaax]";
			mes "O quê...?";
			mes "Você não vê que essa invenção pode mudar para sempre a maneira como as flechas são carregadas?! O futuro é agora!";
			close;
		}
	}
	else {
		mes "[Inventor Jaax]";
		mes "Você pode carregar no máximo "+ .@amount +" flechas nesta aljava. Foi feita usando meu método secreto,";
		mes "então o peso total das flechas e da aljava é menor do que carregar apenas as flechas.";
		next;
		mes "[Inventor Jaax]";
		mes "É um milagre da ciência! Um que você pode experimentar por si mesmo se trouxer pelo menos "+ .@amount +" flechas e "+ .@zeny_req +" Zeny para cada aljava.";
		close;
	}
}
