//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

valkyrie,48,86,4	script	Valkyrie#	811,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valkyrie]";
		mes "Por favor, sinta-se à vontade";
		mes "enquanto estiver aqui.";
		mes "Honra aos guerreiros!";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valkyrie]";
		mes "Agora você encerrará";
		mes "sua vida presente e";
		mes "iniciará uma vida totalmente nova.";
		mes "Honra aos guerreiros!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() != 0 || checkfalcon() != 0 || checkriding() != 0) {
			mes "[Valkyrie]";
			mes "Há algumas coisas que você deve";
			mes "fazer antes de começarmos. Você deve";
			mes "primeiro esvaziar sua mente e corpo.";
			mes "A honra vem quando você abandona";
			mes "todos os seus desejos egoístas...";
			next;
			mes "[Valkyrie]";
			mes "Você não pode levar nada consigo";
			mes "para a próxima vida. Seus itens, zeny, pets e Carrinho precisam ser deixados para trás.";
			next;
			mes "[Valkyrie]";
			mes "Quando estiver pronto,";
			mes "por favor, retorne até mim,";
			mes "bravo aventureiro.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "Vejo que você já";
			mes "se libertou de";
			mes "todos os apegos mundanos,";
			mes ""+ strcharinfo(0) +".";
			next;
			mes "[Valkyrie]";
			mes "Isso é uma atitude admirável para um aventureiro como você. A honra vem quando você abandona todos os desejos pessoais em prol da humanidade.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Hmm... Sinto que você tem";
				mes "alguma ligação ou";
				mes "assunto inacabado em sua";
				mes "vida atual. Resolva isso,";
				mes "e dê um encerramento à sua vida presente.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Agora, vou remover todas";
			mes "as suas memórias atuais...";
			mes "No entanto, você será capaz de";
			mes "lembrar dos momentos mais honrados";
			mes "desta vida.";
			next;
			mes "[Valkyrie]";
			mes "Com um,";
			mes "pedirei à";
			mes "deusa Urd para remover";
			mes "todas as suas memórias atuais.";
			next;
			mes "[Valkyrie]";
			mes "Com dois,";
			mes "pedirei à";
			mes "deusa Verdandi para manter";
			mes "e registrar os momentos mais honrados de sua vida atual.";
			next;
			mes "[Valkyrie]";
			mes "Com três,";
			mes "pedirei à";
			mes "deusa Skuld para";
			mes "guiá-lo para a";
			mes "sua próxima vida.";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			mes "Dois......";
			next;
			mes "[Valkyrie]";
			mes "Um...";
			mes "Dois......";
			mes "E Três.";
			set ADVJOB, Class+Job_Novice_High; //anota o ID da 3ª Classe alvo
			if(ADVJOB == Job_Lord_Knight2) set ADVJOB,Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) set ADVJOB,Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			set MISC_QUEST,MISC_QUEST & ~1024;//<-reseta o Evento de Redefinição de Habilidade
			skill "NV_FIRSTAID",1,SKILL_PERM;
			skill "NV_TRICKDEAD",1,SKILL_PERM;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "Parabéns.";
			mes "Você agora renasceu";
			mes "em uma vida totalmente nova.";
			mes "Por favor, aceite esses pequenos presentes";
			mes "em preparação para suas novas aventuras.";
			getitem 1202,1; //Faca_
			getitem 2302,1; //Camisa_de_Algodão_
			next;
			mes "[Valkyrie]";
			mes "Desejo que a liberação que a deusa Urd concedeu a você seja uma bênção. Espero que as memórias que Verdandi registrou sempre o honrem.";
			next;
			mes "[Valkyrie]";
			mes "E oro para que a nova vida para a qual a deusa Skuld o guiará seja ainda mais honrada do que a última.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (checkre(0)) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Bem-vindo";
		mes "a Valhalla,";
		mes "o Salão da Honra.";
		next;
		mes "[Valkyrie]";
		mes "Infelizmente, você ainda não foi convidado aqui. Peço que saia imediatamente. Honra aos guerreiros!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Metheus Sylphe#Library	742,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Metheus Sylphe]";
			mes "Bem-vindo à Biblioteca da Academia de Magia Schweicherbil.";
			mes "Presumo que você tenha vindo aqui";
			mes "para ler o 'Livro de Ymir'.";
			next;
			mes "[Metheus Sylphe]";
			mes "Infelizmente, a cópia original do livro foi danificada ao longo do tempo. Atualmente, permitimos que o público veja uma cópia do livro.";
			next;
			mes "[Metheus Sylphe]";
			mes "Além disso, para preservar o 'Livro de Ymir' original, decidimos aceitar doações de pessoas que desejam ler a cópia que fornecemos.";
			next;
			mes "[Metheus Sylphe]";
			mes "A quantia sugerida";
			mes "de doação é";
			mes "1.285.000 zeny.";
			next;
			if (select("Doar.:Cancelar.") == 1) {
				if (Zeny >= 1285000) {
					set Zeny, Zeny-1285000;
					set valkyrie_Q,1;
					mes "[Metheus Sylphe]";
					mes "Obrigado, sua doação será usada para uma boa causa. Agora você pode";
					mes "entrar e ler o livro.";
					close;
				}
				mes "[Metheus Sylphe]";
				mes "Infelizmente, você parece não ter dinheiro suficiente no momento. Por favor, verifique seus fundos e volte novamente.";
				close;
			}
			mes "[Metheus Sylphe]";
			mes "Leve o tempo que precisar e";
			mes "aproveite suas viagens.";
			close;
		}
		mes "[Metheus Sylphe]";
		mes "Mais uma vez, obrigado por sua generosa doação. Sinta-se à vontade para ler uma cópia do 'Livro de Ymir' no seu tempo livre.";
		close;
	}
	mes "[Metheus Sylphe]";
	mes "Bem-vindo à Biblioteca da Academia de Magia Schweicherbil.";
	mes "Aqui, temos inúmeros livros. Por favor, tire um tempo e olhe ao redor.";
	close;
}

yuno_in02,93,207,1	script	Book of Ymir	111,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[O Livro de Ymir]";
		mes "...A entrada para o Salão da Honra está aberta para quem avançou para sua próxima vida. Ela está lá para ajudar os heróis a decidirem o que desejam fazer e pode levá-los para qualquer lugar deste mundo.";
		next;
		mes "[O Livro de Ymir]";
		mes "No Salão da Honra, tudo está preparado para os heróis. Dizem que qualquer desejo que não possa ser realizado em nossa realidade pode ser concretizado no Salão da Honra.";
		next;
		if (select("Parar de ler.:Continuar lendo.") == 1) {
			mes "[O Livro de Ymir]";
			mes ".....";
			close;
		}
		mes "[O Livro de Ymir]";
		mes "Existe um caminho esquecido que leva ao Salão da Honra, o lugar mais próximo dos céus. O comum nunca descobrirá este lugar...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[O Livro de Ymir]";
			mes "...Portanto, os antigos heróis";
			mes "sempre estavam em angústia, sabendo que";
			mes "eventualmente, eram mortais e";
			mes "passariam deste reino...";
			next;
			mes "[O Livro de Ymir]";
			mes "Não havia documentos,";
			mes "canções ou folclore remanescente que";
			mes "tivesse qualquer informação sobre a vida após a morte. No entanto, recentemente descobri um antigo pergaminho";
			mes "sobre as Valquírias...";
			next;
			mes "[O Livro de Ymir]";
			mes "Valquírias...";
			mes "A lendária";
			mes "anjo guardião.";
			mes "Anjo do Ragnarok.";
			next;
			mes "[O Livro de Ymir]";
			mes "Aventureiros de grande força";
			mes "e bravura serão conduzidos por";
			mes "Valquíria a Valhalla, o Salão";
			mes "da Honra. Lá, eles receberão uma";
			mes "nova vida.";
			next;
			mes "[O Livro de Ymir]";
			mes "Renascidos, viverão novamente como";
			mes "heróis ainda maiores que";
			mes "iluminarão o mundo. Corpos que";
			mes "estavam exaustos serão preenchidos";
			mes "com energia...";
			next;
			mes "[O Livro de Ymir]";
			mes "E suas almas receberão habilidades com o coração de Ymir. No entanto, o coração de Ymir foi totalmente destruído e espalhado por todo o mundo após a batalha por Rune-Midgarts.";
			next;
			mes "[O Livro de Ymir]";
			mes "Eu encontrei uma pequena quantidade de";
			mes "fragmentos do coração de Ymir ao longo de";
			mes "um longo período de tempo. Mas não posso";
			mes "confirmar se a história de Valquíria";
			mes "e Valhalla é verdade apenas";
			mes "através de testes científicos.";
			next;
			mes "[O Livro de Ymir]";
			mes "Portanto, estou deixando este registro na esperança de que alguém no futuro";
			mes "possa confirmar que Valquíria e Valhalla realmente existem...";
			next;
			set valkyrie_Q,2;
			if(checkquest(1000) == -1) {
				setquest 1000;
			}
			mes "[O Livro de Ymir]";
			mes "Que os heróis vivam novas vidas";
			mes "para que possam proteger o mundo";
			mes "do perigo. E então...";
			close;
		}
		mes "[O Livro de Ymir]";
		mes "...";
		close;
	}
	mes "[O Livro de Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	111,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	124,{
	if (Upper != 1) {
		set .@karma_d,rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporter]";
			mes "Parabéns.";
			mes "Honra aos guerreiros!";
			close;
		}
		mes "[Teleporter]";
		mes "Por favor, abstenha-se";
		mes "de tocar em qualquer";
		mes "das exposições.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporter]";
		mes "Honrado,";
		mes "para onde você deseja ir?";
		next;
		switch(select("Prontera:Morocc:Payon:Geffen:Alberta:Izlude:Al De Baran:Comodo:Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (checkre(0)) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_WarpChar:
	mes "[Teleporter]";
	mes "Tenha uma boa viagem.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2),1,1;
	warp getarg(0),getarg(1),getarg(2);
	end;
}

// Warps
//============================================================ 
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75

