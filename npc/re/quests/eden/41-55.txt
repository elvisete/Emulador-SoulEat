//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

moc_para01,40,38,3	script	Missões [41 - 55]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Você precisa ter -";
		mes "- o ^4d4dff'emblema do Éden'^000000 -";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Espere um momento!!";
		mes "Você não pode receber mais itens porque está carregando muitos peso. Por favor, tente novamente depois de guarda alguns itens.^000000";
		close;
	}
	if (checkquest(12088) == -1) {
		if (BaseLevel < 41) {
			mes " - Você não tem nível suficiente - ";
			mes " - para esse quadro de missões. - ";
			close;
		}
		if (BaseLevel > 55) {
			mes " - Essas missões são muito - ";
			mes " - fáceis para você. - ";
			mes " - Procure uma missão mais - ";
			mes " - adequada para o seu nível. - ";
			close;
		}
		mes "[Quadro de Missões]";
		mes "Quadro de avisos das missões.";
		next;
		if(select("» Conferir as missões:» Cancelar") == 2) {
			mes "^000077Ok. fica para uma próxima vez.^000000.";
			close;
		}
		switch(select("» Caçar Sapo de Thara:» Caçar Soldadinho de Chumbo:» Caçar os Kukres:» Caçar Filhote de Orc:» Caçar Guerreiros Orc:» Caçar Vadon:» Caçar Megalodon:» Caçar Marte:» Caçar Cornutu:» Caçar Névoa:» Caçar os Moluscos:» Caçar Esfera Marinha:» Caçar Fen:» Caçar Orcs Esqueleto:» Caçar Zenorc:» Caçar Múmia")) {
			case 1: callsub L_Quest, 12072, "Você já viu um sapo vermelho? Ele é chamado de Sapo de Thara e é muito raro. Estamos tentando verificar onde ele habita. Por favor, caçe 20 Sapo de Thara.";
			case 2: callsub L_Quest, 12073, "Ouvi falar de alguns brinquedos atacando pessoas. É ridículo! Vamos mostrar a eles nosso poder! Temos que caçar 15 Soldadinhos de Chumbo!";
			case 3: callsub L_Quest, 12074, "Alguém foi roubado perto da praia! O possível suspeito é um monstro criminoso chamado Kukre. Por favor, caçe 30 Kukres.";
			case 4: callsub L_Quest,12075,"Os Filhote de Orc estão ficando mais violentos ultimamente. Por favor, caçe 15 Filhotes de Orc.";
			case 5: callsub L_Quest,12076,"Os Guerreiros Orcs estão ficando mais agressivos. Por favor, caçe 20 Guerreiros Orcs.";
			case 6: callsub L_Quest,12077,"Você já viu caranguejos com conchas grossas? Eles são Vadons. Por favor, caçe 15 Vadons.";
			case 7: callsub L_Quest,12078,"Peixes mortos estão por toda a praia e está piorando. Por favor, caçe 30 Megalodons.";
			case 8: callsub L_Quest,12079,"Você pode caçar Marte? Eu preciso de 15 Martes para estudos. Por favor, me ajude.";
			case 9: callsub L_Quest,12080,"Um Cornutu idiota mordeu o pé do Prefeito. O Prefeito está chateado, então precisamos caçar 20 Cornutus para deixá-lo feliz!";
			case 10: callsub L_Quest,12081,"As pessoas estão pirando por causa da Névoa. Ela parece um fantasma, então assusta as pessoas. Por favor, caçe 15 Névoas.";
			case 11: callsub L_Quest,12082,"Não pergunte por que, mas por favor, caçe 30 Moluscos. É uma missão secreta!";
			case 12: callsub L_Quest,12083,"Esfera Marinha? Você sabe o que é uma Esfera Marinha? Eu tenho uma missão para você caçar 15 Esferas Marinhas, então por favor, faça isso.";
			case 13: callsub L_Quest,12084,"O prefeito quer comer um prato de peixe delicioso. Fen será o ingrediente perfeito para isso. Por favor, caçe 20 Fen!";
			case 14: callsub L_Quest,12085,"Monstros Orcs mortos voltaram para nossa cidade e ressuscitaram como Orcs Esqueletos. Estamos em caos! Por favor, caçe 15 Orcs Esqueletos.";
			case 15: callsub L_Quest,12086,"Alguns monstros estranhos chamados Zenorc estão nos atacando. Por favor, caçe 30 Zenorcs.";
			case 16: callsub L_Quest,12087,"Esteja preparado! É uma missão cruel! Caçe 15 Múmias!! Eu tentei uma vez, mas não consegui. É muito assustador!";
		}
		end;
	}
	setarray .@Exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(set .@i,0; .@i<16; set .@i,.@i+1)
		if (checkquest(12072+.@i,HUNTING) == 2) {
			mes "[Quadro de Missões]";
			mes "Você completou a missão.";
			mes "Aqui está sua recompensa.";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect2 EF_STEAL;
			getexp .@Exp[.@i],0;
			close;
		}
	if ((checkquest(12072,HUNTING) == 0) && (checkquest(12073,HUNTING) == 0) && (checkquest(12074,HUNTING) == 0) && (checkquest(12075,HUNTING) == 0) && (checkquest(12076,HUNTING) == 0) && (checkquest(12077,HUNTING) == 0) && (checkquest(12078,HUNTING) == 0) && (checkquest(12079,HUNTING) == 0) && (checkquest(12080,HUNTING) == 0) && (checkquest(12081,HUNTING) == 0) && (checkquest(12082,HUNTING) == 0) && (checkquest(12083,HUNTING) == 0) && (checkquest(12084,HUNTING) == 0) && (checkquest(12085,HUNTING) == 0) && (checkquest(12086,HUNTING) == 0) && (checkquest(12087,HUNTING) == 0)) {
		mes "Agora você pode visualizar o mural de avisos.";
		erasequest 12088;
		close;
	}
	mes "Não há nada de especial para fazer.";
	close;
L_Quest:
	mes "[Aviso do Quadro de Missões]";
	mes getarg(1);
	next;
	if(select("Aceitar missão.:Cancelar.") == 1) {
		mes "[Quadro de Missões]";
		mes "Você aceitou a missão. Para mais detalhes, pressione as teclas 'alt + u', para abrir a Janela de Missão.";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
