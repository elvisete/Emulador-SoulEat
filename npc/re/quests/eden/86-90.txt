//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

moc_para01,48,175,3	script	Missões [86 - 90]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Você precisa ter -";
		mes "- o ^4d4dff'emblema do Éden'^000000 -";
		next;
		switch(select("Apenas saia.:Missões do ^0000FFGrupo Éden^000000?:Como posso ser membro do ^0000FFGrupo Éden^000000?")) {
		case 1:
			close;
		case 2:
			mes "::As Missões do Grupo Éden";
			mes "::basicamente são missões";
			mes "::solicitadas por";
			mes "::pessoas de todo o reino.";
			mes "::Nós apenas as categorizamos.";
			next;
			mes "::No Grupo Éden,";
			mes "::classificamos as missões";
			mes "::de acordo com o nível de dificuldade.";
			mes "::De acordo com esse nível,";
			mes "::você pode encontrar";
			mes "::missões que";
			mes "::se adequam a você.";
			next;
			mes "::Quando você terminar,";
			mes "::você pode voltar";
			mes "::para o quadro de missões.";
			next;
			mes "::Então você será";
			mes "::recompensado!!";
			mes "::Simples, não é!!";
			mes "::Nenhuma comissão necessária!!";
			close;
		case 3:
			mes "::Você pode se tornar um membro";
			mes "::do Grupo Éden";
			mes "::basta falar com ^0000FFSecretaria Lenore^000000,";
			mes "::que esta no 1º andar.";
			next;
			mes "::Se você se tornar um membro,";
			mes "::Você receberá um";
			mes "\"^006400Emblema do Éden^000000\".";
			mes "::Então você sera capaz";
			mes "::de ver o quadro de Missões.";
			close;
		}
	}
	if (BaseLevel < 86) {
	mes " - Você nao tem nivel suficiente - ";
	mes " - para esse quadro de Missões. - ";
	close;
	} else if (BaseLevel > 90) {
		// Quest IDs: 4167-4180
		// Quest cooldown IDs: 4198-4211
		setarray .@names$[0],
			"Dragon Tail","Spring Rabbit","Pest",
			"Bathory","Alarm",
			"Baba Yaga","Yao Jun","Firelock Soldier","Zipper Bear",
			"Ground Petite","Rafflesia","Venomous","Pitman","Yellow Novus";
		setarray .@exp[0],
			76000,82000,82000,
			72000,76000,
			72000,78000,82000,80000,
			72000,72000,72000,86000,80000;
			mes " - Essas Missões sao muito - ";
			mes " - faceis para você. - ";
			mes " - Procure uma missao mais - ";
			mes " - adequada para o seu nivel. - ";
		for(set .@quest,4167; .@quest<=4180; set .@quest,.@quest+1) {
			set .@hunting, checkquest(.@quest,HUNTING);
			if (.@hunting == 0 || .@hunting == 1) {
				next;
				mes ":: Você não pode prosseguir ";
				mes ":: Caçando ^0000FF"+.@names$[.@quest-4167]+"^000000.";
				mes ":: O registro para a missão";
				mes ":: foi cancelada.";
				erasequest .@quest;
				close;
			} else if (.@hunting == 2) {
				next;
				mes ":: Você adicionou informações";
				mes ":: sobre a missão";
				mes ":: Caçando ^0000FF"+.@names$[.@quest-4167]+"^000000";
				mes ":: no quadro de missões.";
				mes ":: Você obteve ";
				mes ":: experiência.";
				erasequest .@quest;
				getexp .@exp[.@quest-4167],0;
				close;
			}
		}
		close;
	}
	mes "[Quadro de Missões]";
	mes "Existem várias solicitações neste quadro.";
	next;
	switch(select("» ^8B4513Ilha Tartaruga^000000:» ^8B4513Torre do Relógio^000000:» ^8B4513Cidades de Expansão^000000:» ^8B4513Outras Regiões^000000")) {
	case 1:
		mes "^8B4513Ilha Tartaruga^000000.";
		mes "Qual monstro você caçará ou já terminou de caçar?";
		next;
		switch(select("» ^0000FFLavadeira^000000:» ^0000FFCoelho^000000:» ^0000FFPeste^000000")) {
			case 1: callsub L_Quest,4167,"Lavadeira",76000,"µå·¡°ïÅ×ÀÏÄ«µå","Há um monstro que só pode ser encontrado na ^8B4513Ilha Tartaruga^000000.","s";
			case 2: callsub L_Quest,4168,"Coelho",82000,"½ºÇÁ¸µ·¡ºøÄ«µå","Há um monstro que só pode ser encontrado na ^8B4513Ilha Tartaruga^000000.","s";
			case 3: callsub L_Quest,4169,"Peste",82000,"Æä½ºÆ®Ä«µå","Há um monstro que só pode ser encontrado na ^8B4513Ilha Tartaruga^000000.","s";
		}
	case 2:
		mes "^8B4513Torre do Relógio^000000.";
		mes "Qual monstro você caçará ou já terminou de caçar?";
		next;
		switch(select("» ^0000FFBathory^000000:» ^0000FFAlarme^000000")) {
			case 1: callsub L_Quest,4170,"Bathory",72000,"¹Ù¼Ò¸®Ä«µå","A Torre do Relógio está cheia de monstros perigosos.";
			case 2: callsub L_Quest,4171,"Alarme",76000,"¾Ë¶÷Ä«µå","A Torre do Relógio está cheia de monstros perigosos.";
		}
	case 3:
		mes "^8B4513Cidades de Expansão^000000";
		mes "Qual monstro você caçará ou já terminou de caçar?";
		next;
		switch(select("» ^0000FFBaba-Yaga^000000:» ^0000FFYao Jun^000000:» ^0000FFSoldado de Trincheira^000000:» ^0000FFUrso Zipper^000000")) {
			case 1: callsub L_Quest,4172,"Baba-Yaga",72000,"","A bruxa Baba-Yaga pode ser encontrada nos arredores de Moscovia.";
			case 2: callsub L_Quest,4173,"Yao Jun",117000,"Çý±ºÄ«µå","Há monstros semelhantes a humanos que vagam pelo Calabouço de Luoyang.";
			case 3: callsub L_Quest,4174,"Soldado Atirador",82000,"±¸½ÄÁ¶ÃÑº´Ä«µå","Há monstros semelhantes a humanos que vagam pelo Calabouço de Amatsu.","s";
			case 4: callsub L_Quest,4175,"Zipper",120000,"ÁöÆÛº£¾îÄ«µå","Há monstros em formas de urso se reunindo no Calabouço de Kunlun.";
		}
	case 4:
		mes "[Quadro de Missões]";
		mes "Aqui está uma lista de solicitações aleatórias para caçar monstros em outras regiões de Midgard.";
		mes "Qual monstro você caçará ou já terminou de caçar?";
		next;
		switch(select("» ^0000FFPetite^000000:» ^0000FFRafflesia^000000:» ^0000FFNuvem Venenosa^000000:» ^0000FFMineiros^000000:» ^0000FFNovus^000000")) {
			case 1: callsub L_Quest,4176,"Petite",72000,"Áö»ó»Ú¶ìÄ«µå","Há monstros do tipo dragão invadindo a região oeste de Geffen.";
			case 2: callsub L_Quest,4177,"Rafflesia",72000,"¶óÇÃ·¹½Ã¾ÆÄ«µå","Há monstros do tipo planta infestando os campos de Lighthalzen.";
			case 3: callsub L_Quest,4178,"Nuvem Venenosa",72000,"º£³ë¸Ó½º","Há monstros do sem forma flutuando pelos campos de Einbroch.";
			case 4: callsub L_Quest,4179,"Mineiros",86000,"ÇÍ¸Ç","Há monstros não-mortos empurrando um carrinho pelos campos e Calabouços de Einbroch.";
			case 5: callsub L_Quest,4180,"Novus",80000,"¿»·Î¿ì³ë¹ö½º","Há monstros do tipo dragão amarelo aterrorizando os campos de Hugel.";
		}
	}
	end;

//callsub L_Quest,<quest ID>,"<monster name>",<reward EXP>,"<cutin image>","<description>"{,"s"};
L_Quest:
	set .@quest1, getarg(0);
	set .@quest2, .@quest1+31;
	.@cutin$ = getarg(3);
	set .@playtime, checkquest(.@quest2,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[Quadro de Missões]";
		mes "Você precisa esperar 3 horas antes de pegar esta missão novamente.";
		close;
	}
	set .@hunting, checkquest(.@quest1,HUNTING);
	if (.@hunting == 0 || .@hunting == 1) {
		mes "[Quadro de Missões]";
		mes "Você terminou de caçar todos os 30 "+getarg(1)+getarg(5,"")+"?";
		close;
	} else if (.@hunting == 2) {
		mes "[Quadro de Missões]";
		mes "Você completou a caçada.";
		mes "Por favor, aceite esta recompensa como recompensa.";
		erasequest .@quest1;
		if (.@playtime > -1) erasequest .@quest2;
		setquest .@quest2;
		getexp getarg(2),0;
		close;
	}/* else if (.@hunting == 3) {
//		recall_completequest .@quest1
		erasequest .@quest1;
		mes "[Quadro de Missões]";
		mes "Você reaceitou a missão de caça "+getarg(1)+" novalmente.";
		close;
	}*/ else {
		mes "[Quadro de Missões]";
		mes getarg(4);
		next;
		if (.@cutin$ != "") cutin .@cutin$,3;
		mes "[Quadro de Missões]";
		mes "Cace 30 "+getarg(1)+" e poste seu sucesso neste quadro.";
		next;
		if (.@cutin$ != "") cutin .@cutin$,255;
		if(select("Sim, aceitar.:Não, obrigado.") == 1) {
			mes "[Quadro de Missões]";
			mes "Quando você completar a missão, poste-a neste quadro e receba sua recompensa.";
			if (.@playtime > -1) erasequest .@quest2;
			setquest .@quest1;
		}
		close;
	}
}
