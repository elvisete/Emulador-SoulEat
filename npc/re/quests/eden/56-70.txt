//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

moc_para01,42,38,3	script	Missões [56 - 70]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Você precisa ter -";
		mes "- o ^4d4dff'emblema do Éden'^000000 -";
		close;
	}
	if (BaseLevel < 56) {
		mes " - Você não tem nível suficiente - ";
		mes " - para esse quadro de missões. - ";
		close;
	}
	if (BaseLevel > 70) {
		mes " - Essas missões são muito - ";
		mes " - fáceis para você. - ";
		mes " - Procure uma missão mais - ";
		mes " - adequada para o seu nível. - ";
		close;
	}
	switch(select("» Missões de 1~5:» Missões de 6~10:» Missões de 11~15")) {
	case 1:
		switch(select("» O que é esta Bandagem?:» Crocodilo Perigosos.:» Aquilo não era uma sereia...:» Meu país está me chamando!")) {
			case 1: callsub L_HuntingQuest,3250,15000,5000,930,30;
			case 2: callsub L_HuntingQuest,3251,16000,6000;
			case 3: callsub L_HuntingQuest,3252,17000,8000;
			case 4: callsub L_Quest,3254,18000,8000,7043,10,1056,30;
		}
	case 2:
		switch(select("» Caçar Selvagem:» Acampamento de Verão:» Rancor de uma Mulher:» Ingredientes Especiais:» Agonia do Mestre de Marionetes")) {
			case 1: callsub L_HuntingQuest,3255,16000,7000;
			case 2: callsub L_Quest,3256,15000,5000,918,30;
			case 3: callsub L_HuntingQuest,3257,17000,8000;
			case 4: callsub L_Quest,3258,16000,7000,7198,40;
			case 5: callsub L_HuntingQuest,3259,17000,7000,1060,30;
		}
	case 3:
		switch(select("» Moscas Caçadora:» Munak Perigoso:» Deixe o Mundo Verde:» Varinha Mágica")) {
			case 1: callsub L_HuntingQuest,3260,16000,6000;
			case 2: callsub L_HuntingQuest,3261,16000,7000;
			case 3: callsub L_Quest,3262,17000,7000,1032,40;
			case 4: callsub L_Quest,3263,18000,8000,1021,30,7150,30;
		}
	}
	end;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "Você gostaria de aceitar esta missão?";
		next;
		if(select("» Sim:» Não") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "Essas missões não são adequadas para o seu nível.";
				close;
			}
			setquest .@quest_id;
			mes "Aceita com sucesso. quando concluir, volte e confirme seu trabalho.";
			next;
			mes "Esta missão não tem limites de tempo.";
		}
		close;
	}
	if (getargcount() > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id) < 2 && (countitem(getarg(3)) < getarg(4) || .@items == 1)) {
		mes "Você tem uma missão em andamento. Gostaria de verificar os detalhes?";
		next;
		if(select("Verificar.:Não, obrigado.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else {
		mes "Consegui me sair muito bem na missão. Devo relatá-la agora?";
		next;
		if(select("Relatar a missão.:Ainda não.") == 1) {
			delitem getarg(3),getarg(4);
			if (.@items)
				delitem getarg(5),getarg(6);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "Você completou a missão. Receba as recompensas.";
		}
		close;
	}
	end;

L_HuntingQuest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "Você gostaria de aceitar esta missão?";
		next;
		if(select("» Sim:» Não") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "Essas missões não são adequadas para o seu nível.";
				close;
			}
			setquest .@quest_id;
			mes "Aceita com sucesso. quando concluir, volte e confirme seu trabalho.";
			next;
			mes "Esta missão não tem limites de tempo.";
		}
		close;
	}
	if (getargcount() > 3) {
		if (countitem(getarg(3)) < getarg(4))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id,HUNTING) < 2 || .@items == 1) {
		mes "Você tem uma missão em andamento. Gostaria de verificar os detalhes?";
		next;
		if(select("Verificar.:Não, obrigado.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else if (checkquest(.@quest_id,HUNTING) == 2) {
		mes "Consegui me sair muito bem na missão. Devo relatá-la agora?";
		next;
		if(select("Relatar a missão.:Ainda não.") == 1) {
			if (.@items)
				delitem getarg(3),getarg(4);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "Você completou a missão. Receba as recompensas.";
		}
		close;
	}
	end;

L_Details:
	switch(getarg(0)) {
	case 3250:
		mes "Esta missão é atribuída pela corporação Rekenber de Lighthalzen. Abaixo estão os detalhes.";
		next;
		mes "A corporação Rekenber está prestes a lançar um novo projeto chamado 'Centro Médico Verde'. Para o primeiro produto do projeto, estamos preparando Bandagens de Bem-Estar.";
		next;
		mes "Com relação ao projeto, precisamos pesquisar as reações dos monstros quando são envolvidos com Bandagem Estragada.";
		next;
		mes "Se você estiver interessado, por favor, caçe 30 Múmias e traga 30 Bandagem Estragada. Você pode encontrá-los na Pirâmide a oeste de Morocc.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "- Diretora-chefe da corporação Rekenber 'Julie E Delph' -";
		break;
	case 3251:
		mes "Esta missão é atribuída pela sociedade cooperativa de Comodo. Abaixo estão os detalhes.";
		next;
		mes "Os Crocodilos têm ficado selvagens ultimamente, eles atacam mulheres e pessoas bêbadas que ficam perto da praia.";
		next;
		mes "Esta é uma situação muito vergonhosa para nós, o local de férias mais movimentado de Rune-Midgarts.";
		next;
		mes "Portanto, seria muito útil se você caçasse 30 Crocodilos para resolver essa situação.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		break;
	case 3252:
		mes "Esta missão é atribuída por um cliente desconhecido de Izlude.";
		next;
		mes "Tenho admirado a praia de Izlude desde que nasci.";
		next;
		mes "Quando eu era jovem, encontrei uma criatura misteriosa na masmorra de Izlude, nunca tinha visto algo assim antes...";
		next;
		mes "Instantaneamente, pensei que fosse uma sereia. Fiquei tão surpreso, estudei sobre sereias durante toda a minha vida.";
		next;
		mes "Mas, conforme estudei mais, descobri que a criatura não era uma sereia... sim, não era bonita como as outras sereias...";
		next;
		mes "Finalmente descobri que era um Tritão, não uma sereia!";
		next;
		mes "Tritão... desperdicei minha vida estudando esse monstro! Então, por favor, caçe 30 Tritões para me fazer se sentir melhor.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Izlude 'A sereia dos sonhos perdidos' --";
		break;
	case 3254:
		mes "Esta missão é atribuída pelo oficial do governo do Reino de Rune-Midgarts.";
		next;
		mes "Para evitar uma inundação em Prontera, decidimos construir uma grande barragem para regular o abastecimento de água.";
		next;
		mes "Mas estamos com falta de suprimentos e precisamos coletar Areia Fina e Torroes de Areia.";
		next;
		mes "Então, gostaríamos de pedir ajuda. Pessoas, por favor, nos tragam 10 Areias Finas e 30 Torroes de Areia, para que possamos construir a barragem.";
		next;
		mes "Você pode obter esses materiais do Arenoso, desejo-lhe boa sorte!";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Oficial do governo do Reino de Rune-Midgarts, Dufre Kent --";
		break;
	case 3255:
		mes "Esta missão é atribuída pela prefeitura de Payon.";
		next;
		mes "Animais selvagens sempre são uma ameaça para os agricultores.";
		next;
		mes "Eles atacam nossa fazenda e destroem toda a colheita!! Não podemos mais tolerar isso!";
		next;
		mes "Então, gostaríamos de pedir ajuda dos bravos aventureiros.";
		next;
		mes "Por favor, cace 30 Selvagens para salvar nossas colheitas!";
		next;
		mes "Você pode facilmente encontrar esses monstros ao redor de nossa cidade e campos.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Membros da prefeitura de Payon --";
		break;
	case 3256:
		mes "Esta missão é atribuída pelo ferreiro de Morocc, Aragham.";
		next;
		mes "Olá~~~~, Aventureiros! Aqui é o Aragham, o ferreiro mais quente de Morocc!";
		next;
		mes "De qualquer forma, tenho um filho chamado Aragam Jr. O bonitinho. Haha.";
		next;
		mes "Ele vai participar do acampamento de verão neste verão, mas ele não sabe nadar.";
		next;
		mes "Eu quero ensiná-lo, mas como você sabe, eu sou um ferreiro, longe de ser um nadador! Hehe.";
		next;
		mes "Mas, pensei que se eu fizesse nadadeiras, seria ótimo para a confiança dele~!";
		next;
		mes "Para fazer isso, preciso de 30 Patas Pegajosas de um Sapo de Thara. Você pode trazê-las para mim?";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "PS. Não me compare com Hollgrehenn ou Antonio!!!!";
		next;
		mes "-- O ferreiro mais quente, ferreiro de Morocc Aragham --";
		break;
	case 3257:
		mes "Esta missão é atribuída pelo inventor Dorian de Izlude.";
		next;
		mes "Você já ouviu a história de que se as mulheres têm ressentimento em suas mentes, isso trará desastres naturais.";
		next;
		mes "Uau, então os caras devem ficar de olho nessas mulheres sensíveis! Certifique-se de que elas não têm ressentimento contra você.";
		next;
		mes "Mulheres podem trazer fenômenos estranhos com elas!";
		next;
		mes "Estou tão intrigado com essa história que estou tentando provar que pode ser verdade.";
		next;
		mes "Então, preciso deixar as mulheres chateadas! Haha, eu sei que é uma ideia ruim, certo?";
		next;
		mes "Mas isso é sério apenas para estudar... Então, por favor, cace 30 Sohee's que parecem ter sido revividas.";
		next;
		mes "Para que eu possa continuar observando o caso.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Inventor de elegância Dorian --";
		break;
	case 3258:
		mes "Esta missão é atribuída por Wallah de Payon.";
		next;
		mes "Olá, aventureiro. Como você está? Como está sua saúde?";
		next;
		mes "Eu sou uma farmacêutica e estou tendo dificuldade para conseguir ingredientes especiais ultimamente.";
		next;
		mes "Porque eu sou uma garota um pouco sensível, como posso conseguir essas coisas sozinha.";
		next;
		mes "Então, por favor, me ajude. Basta trazer 40 Folhas Gigantes para mim, isso será o suficiente para completar meu remédio especial!";
		next;
		mes "Por favor, traga-o o mais rápido possível, as pessoas precisam do meu remédio.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Wallah --";
		break;
	case 3259:
		mes "Esta missão é atribuída pelo mestre de marionetes Woonute de Geffen.";
		next;
		mes "Você sabe que ser um mestre de marionetes é meio que um trabalho difícil para satisfazer as pessoas.";
		next;
		mes "As pessoas podem procurar e ver tantas coisas diferentes por conta própria nos dias de hoje.";
		next;
		mes "Suas expectativas estão aumentando, não consigo mais satisfazê-las.";
		next;
		mes "Mas se eu puder criar novos conceitos de brinquedos, como brinquedos vivos, será inovador.";
		next;
		mes "Ouvi dizer que existem bonecos vivos chamados Marionetes. Você pode caçar 30 Marionetes para mim? E também trazer 30 Cabelos Dourados, esses serão ótimos materiais para os novos brinquedos.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Mestre de Marionetes Woonute --";
		break;
	case 3260:
		mes "Esta missão é atribuída por um exterminador de Prontera.";
		next;
		mes "À medida que o clima esquenta, os insetos se multiplicam cada vez mais. Já está fora de controle.";
		next;
		mes "As pessoas em Prontera não conseguem dormir por causa de todos os insetos em suas casas.";
		next;
		mes "Então, por favor, nos ajude, caçando 30 Moscas Caçadoras. Assim, as moscas podem ser reduzidas aos poucos.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- O exterminador de Prontera --";
		break;
	case 3261:
		mes "Esta missão é atribuída por Cheese Dongja de Payon.";
		next;
		mes "Você já ouviu falar sobre Munak?";
		next;
		mes "O monstro feio Munak está ameaçando as pessoas em Payon.";
		next;
		mes "Então precisamos de bravos aventureiros como você!";
		next;
		mes "Por favor, cace 30 Munaks e então Payon estará em paz.";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "- Payon Cheese Dongja -";
		break;
	case 3262:
		mes "Esta missão é atribuída por um negociante de gemas, Ibraham de Morocc.";
		next;
		mes "Uma pequena e bela flor no deserto árido... Que cena incrível seria!";
		next;
		mes "Plantar flores podem deixar o mundo verde e seria tão bonito em todos os lugares.";
		next;
		mes "Planejo plantar flores fortes no deserto de Morocc para que o solo melhore.";
		next;
		mes "Eu preciso de 40 Flores de Planta Carnivora, eu sei que a Planta Carnivora é uma flor feia, mas ainda é uma planta.";
		next;
		mes "Espero que você possa caçar Planta Carnivora e me trazer 40 Flores de Planta Carnivora. Você está tornando o mundo verde! Se orgulhe!";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Morocc Ibraham --";
		break;
	case 3263:
		mes "Esta missão é atribuída pelo ferreiro de Payon, Antonio.";
		next;
		mes "Varinha mágica! Você já ouviu falar sobre isso? É uma varinha incrível!";
		next;
		mes "Se você disser as palavras mágicas e balançar a varinha, ela realizará seus desejos.";
		next;
		mes "Então, decidi fazer uma por mim mesmo. Então, eu serei rico e poderoso~! Haha!!";
		next;
		mes "Para fazer a varinha, eu preciso de alguns materiais especiais.";
		next;
		mes "Por favor, traga para mim 30 Chifres de Dokebi e 30 Troncos. Se você fizer isso, eu emprestarei minha varinha mágica uma vez. Haha, bom negócio?";
		next;
		mes "Por favor, nos confirme após completar a missão, então daremos uma recompensa apropriada.";
		next;
		mes "-- Ferreiro de Payon, Antonio --";
		break;
	}
	return;
}
