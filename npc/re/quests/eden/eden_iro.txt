//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

// Acolyte Warper (part of other/acolyte_warp.txt)
//============================================================
moc_para01,14,32,5	script	Aperture#acolytewarp	90,{
	mes "[Abertura]";
	mes "Olá, aventureiro.";
	mes "Tenho estudado magia em toda Midgard para aprimorar o que acredito ser uma das maiores habilidades disponíveis para a classe acólito.";
	next;

	if (is_function("VIP_iRO_Acólito"))
		callfunc "VIP_iRO_Acólito","[Abertura]","Prontera";

	mes "[Abertura]";
	mes "Eu sou o único Acólito que atingiu a habilidade Portal de Dobra de Nível 10!";
	emotion ET_THINK, playerattached();
	next;
	mes "[Abertura]";
	mes "Isso mesmo! ";
	mes "Prometo que não esqueço locais que já memorizei.";
	mes "Um dia irei aprimorar minhas habilidades para ir para onde eu quiser~";
	next;
	mes "[Abertura]";
	mes "Estou disposto a levá-lo para os muitos locais que memorizei por uma pequena taxa.";
	mes "Gostaria de usar este serviço?";
	next;
	switch(select("Sim não")) {
	case 1:
		mes "[Abertura]";
		mes "Para onde você gostaria de ir?";
		mes "Desejo-lhe boa sorte em sua jornada.";
		next;

		setarray .@towns$[0], "Prontera", "Izlude", "Geffen", "Payon", "Marrocos", "Alberta", "Al De Baran", "Comodo", "Umbala", "Juno", "Einbroch", "Lighthalzen", "Enorme", "Rachel";
		setarray .@cost[0],          600,      600,     1200,    1200,     1200,      1800,          2200,     2200,     2200,   1800,       2200,          2200,    2200,     2200;

		set .@size, getarraysize(.@towns$);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+.@towns$[.@i]+"->"+.@cost[.@i]+"z:";
		set .@i, select(.@menu$+"Cancelar")-1;
		if (.@i == .@size) {
			mes "[Abertura]";
			mes "Volte quando precisar de uma dobra.";
			close;
		}
		if (Zeny < .@cost[.@i]) {
			mes "[Abertura]";
			mes "Sinto muito, mas você não tem";
			mes "zeny suficiente para o Teleporte";
			mes "Serviço. ";
			mes "para"+.@towns$[.@i]+"é"+.@cost[.@i]+"Zeny.";
			close;
		}
		specialeffect2 EF_READYPORTAL;
		specialeffect2 EF_TELEPORTATION;
		specialeffect2 EF_PORTAL;
		next;
		set Zeny, Zeny - .@cost[.@i];
		switch(.@i) {
			case 0: warp "Prontera",116,72; break;
			case 1: warp "izlude",128,98; break;
			case 2: warp "Geffen",120,39; break;
			case 3: warp "pagamento",161,58; break;
			case 4: warp "Marrocos",156,46; break;
			case 5: warp "Alberta",117,56; break;
			case 6: warp "aldebarã",168,112; break;
			case 7: warp "comodo",209,143; break;
			case 8: warp "umbala",100,154; break;
			case 9: warp "Yuno",158,125; break;
			case 10: warp "einbroch",67,195; break;
			case 11: warp "lighthalzen",159,90; break;
			case 12: warp "enorme",98,150; break;
			case 13: warp "Rachel",119,135; break;
		}
		close;
	case 2:
		mes "[Abertura]";
		mes "Volte quando precisar de uma dobra.";
		close;
	}
}

function	script	VIP_iRO_Acolyte	{
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes getarg(0);
		mes "Mas, todos os meus poderes estão disponíveis apenas para aqueles que são VIPs.";
		mes "Eu ainda posso lhe oferecer uma urdidura para"+getarg(1)+"no entanto.";
		next;
		switch(select(getarg(0)+"-> 1800z:Cancelar")) {
		case 1:
			if (Zeny < 1800) {
				mes getarg(0);
				mes "Sinto muito, mas você não tem";
				mes "zeny suficiente para o Teleporte";
				mes "Serviço. ";
				mes "para"+getarg(1)+"é 1.800 zeny.";
				close;
			}
			specialeffect2 EF_READYPORTAL;
			specialeffect2 EF_TELEPORTATION;
			specialeffect2 EF_PORTAL;
			next;
			set Zeny, Zeny - 1800;
			if (getarg(1) == "Juno")
				warp "Yuno",158,125;
			else
				warp "Prontera",116,72;
			close;
		case 2:
			close;
		}
	}
	return;
}


// Primo d'Buffer, VIP stat bonuses
//============================================================
moc_para01,26,15,3	script	Primo d'Buffer	790,{
	mes "[Primeiro do Buffer]";
	mes "Olá aventureiro, parece que você está ansioso para sair e salvar o mundo!";
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes "Também posso dizer que você não foi abençoado com '^6666ffVIP Status^000000'.";
		next;
		mes "[Primeiro do Buffer]";
		mes "Minhas ordens são para ajudar qualquer Aventureiro marcado como VIP com Bônus especiais!";
		mes "Atualmente eu lhe darei ^ff0000 7 para todas as suas estatísticas por 30 minutos^000000 se você for VIP.";
		next;
		mes "[Primeiro do Buffer]";
		mes "Se você quiser ser VIP, você pode usar o WarpPortal para ^FF8C42Power-UP^000000 sua conta de jogo com status VIP. ";
		mes "Boas Jornadas!";
		close;
	}
	mes "Também posso dizer que você foi abençoado com '^007FFFVIP Status^000000'.";
	next;
	mes "[Primeiro do Buffer]";
	mes "Isso é incrível!";
	mes "Aqui estão 7 de todas as suas estatísticas por 30 minutos para ajudá-lo em sua jornada. ";
	close2;
	sc_start SC_FOOD_STR_CASH,1200000,7;
	sc_start SC_FOOD_AGI_CASH,1200000,7;
	sc_start SC_FOOD_VIT_CASH,1200000,7;
	sc_start SC_FOOD_DEX_CASH,1200000,7;
	sc_start SC_FOOD_INT_CASH,1200000,7;
	sc_start SC_FOOD_LUK_CASH,1200000,7;
	end;
}

// Clearchus, VIP mercenary rentals
//============================================================
moc_para01,53,30,3	script	Clearchus	470,{
	if (checkquest(62238,PLAYTIME) > -1 && checkquest(62238,PLAYTIME) < 2) {
		mes "Você deve esperar o cronômetro terminar antes de poder contratar um novo mercenário.";
		close;	
	}
	mes "[Clearchus]";
	mes "Saudações jovem aventureiro! ";
	next;
	mes "[Clearchus]";
	mes "Oferecemos treinamento para aspirantes a mercenários, juntando-os a aventureiros como você.";
	next;
	mes "[Clearchus]";
	mes "Nossa organização tem 10.000 pessoas! ";
	next;
	if (!vip_status(VIP_STATUS_ACTIVE)) {
		mes "[Clearchus]";
		mes "Infelizmente parece que você não é VIP, por favor obtenha VIP para poder usar este serviço. ";
		next;
		mes "[Clearchus]";
		mes "Vip é uma ferramenta muito útil para os aventureiros, dando acesso a serviços Kafra extras, warps com desconto, conteúdo exclusivo e uso do Primo d'Buffer. ";
		close;
	}
	mes "[Clearchus]";
	mes "Seu nome está na lista.";
	next;
	mes "[Clearchus]";
	mes "Que tipo de conjunto de habilidades você procura em um mercenário?";
	next;

	switch(select("Espadachim: Arqueiro: Lanceiro")) {
	case 1:
		set .@base_id,12163; //SwordMercenary_Scroll
		break;
	case 2:
		set .@base_id,12153; //Bow_Mercenary_Scroll
		break;
	case 3:
		set .@base_id,12173; //SpearMercenary_Scroll
		break;
	}
	     if (BaseLevel < 11) set .@i,0;
	else if (BaseLevel < 21) set .@i,1;
	else if (BaseLevel < 31) set .@i,2;
	else if (BaseLevel < 41) set .@i,3;
	else if (BaseLevel < 51) set .@i,4;
	else if (BaseLevel < 61) set .@i,5;
	else if (BaseLevel < 71) set .@i,6;
	else if (BaseLevel < 81) set .@i,7;
	else if (BaseLevel < 91) set .@i,8;
	else set .@i,9;

	mes "[Clearchus]";
	mes "Eu tenho apenas o mercenário em mente!";
	next;
	mes "[Clearchus]";
	mes "Divirta-se agora, ouviu?";
	getitem .@base_id + .@i,1;
	if (isbegin_quest(62238) > 0)
		erasequest 62238;
	setquest 62238;
	close;
}

// Carrot & Red Potion Traders
//============================================================
moc_para01,166,51,3	script	Phelix#edco	85,{
	mes "[Félix]";
	mes "Ah... vejo por esse olhar que você quer que eu trabalhe e te dê Carne e Cenoura...";
	mes "Bem, meu amigo Izaac aqui tem algumas Poções Vermelhas com ele, mas eu só tenho algumas Cenouras.";
	mes "Então posso trocar ^0000CC1 cenoura por 3 gelatinas^000000.";
	next;
	mes "[Félix]";
	mes "Se você estiver interessado em minha oferta, compre as gelatinas que mencionei.";
	next;
	select("Cenouras, por favor.");
	mes "[Félix]";
	mes "Tudo bem, vamos ver o que você tem...";
	next;
	if (countitem(909) < 3) {
		mes "[Félix]";
		mes "Hmm, olha cara, eu disse 3 gelatinas por 1 cenoura... entendeu?";
		close;
	}
	mes "[Félix]";
	mes "Não é tão ruim...";
	mes "Quantos você quer?";
	next;
	switch(select("Quantos eu conseguir, por favor.:Eu quero escolher.:Deixa pra lá, eu gosto da minha gelatina.")) {
	case 1:
		set .@amount, countitem(909) / 3;
		break;
	case 2:
		set .@available, countitem(909) / 3;
		mes "[Félix]";
		mes "Quantos você quer?";
		mes "^ff0000Você tem o suficiente para até" + .@available + "Cenouras.^000000";
		input .@input;
		next;
		if (.@input < 1 || .@input > 10000) {
			mes "[Félix]";
			mes "Não quer negociar? ";
			close;
		}
		if (.@input > .@available) {
			mes "[Félix]";
			mes "Cara, eu disse que para cada 3 gelatinas eu te dou 1 cenoura.";
			close;
		}
		set .@amount, .@input;
		break;
	case 3:
		mes "[Félix]";
		mes "Não me incomode se você não quiser negociar.";
		close;
	}
	if (checkweight(515,.@amount) == 0) {
		mes "[Félix]";
		mes "Você está acima do peso.";
		close;
	}
	mes "[Félix]";
	mes "Aí está ~! ";
	mes "Tente não encher a cara.";
	delitem 909,.@amount*3; //Jellopy
	getitem 515,.@amount; //Carrot
	end;
}

moc_para01,163,51,7	script	Izaac#edco	86,{
	mes "[Izaac]";
	mes "Bom dia!";
	mes "Você coletou alguma coisa estranha de monstros?";
	mes "Quero coisas como gelatina, penugem, casca, pena de pássaro, raiz de árvore, casca de minhoca e crisálida.";
	mes "Eu preciso disso para alguma coisa.";
	next;
	mes "[Izaac]";
	mes "Claro, não vou pedir que você me dê essas coisas de graça. ";
	mes "^00CC005 Concha^000000";
	mes "^0000CC10 Penugem^000000";
	mes "^00CC0010 Gelatina^000000";
	mes "^0000CC6 Raiz da árvore^000000";
	mes "^00CC001 Descascamento de minhoca^000000";
	mes "^0000CC7 Pena de Pássaro^000000";
	mes "^00CC006 Crisálida^000000";
	next;
	switch(select("Claro!:Acho que é uma fraude!:Não, obrigado.")) {
	case 1:
		mes "[Izaac]";
		mes "Então, quais itens";
		mes "você quer me trazer?";
		next;

		setarray .@items[0], 935, 914, 909, 902, 955, 916, 915;
		setarray .@count[0],   5,  10,  10,   6,   1,   7,   6;

		set .@size, getarraysize(.@items);
		for(set .@i,0; .@i<.@size; set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@items[.@i])+":";
		set .@select, select(.@menu$+"Cancelar")-1;
		if (.@select == .@size) {
			mes "[Izaac]";
			mes "Claro ~";
			mes "Sem problemas.";
			close;
		}
		set .@item, .@items[.@select];
		set .@item$, getitemname(.@item);
		set .@price, .@count[.@select];

		if (countitem(.@item) < .@price) {
			mes "[Izaac]";
			mes "Ei, onde está tudo";
			mes "que"+.@item$+"que";
			mes "você me prometeu?";
			mes "Me dê"+.@item$+"!";
			close;
		}
		mes "[Izaac]";
		mes "Ok, deixe-me verificar";
		mes "quanto"+.@item$;
		mes "você tem em você.";
		mes "Hum...";
		next;
		set .@amount, countitem(.@item) / .@price;
		set .@trade_amount, .@amount * .@price;
		mes "[Izaac]";
		mes "Você tem";
		mes "um total de"+countitem(.@item)+""+.@item$+"...";
		mes "Para todos eles, posso te dar";
		mes "um total de"+.@amount+"Poção(ões) Vermelha(s).";
		next;
		mes "[Izaac]";
		mes "O que você diz?";
		mes "Nós temos um acordo?";
		next;
		if(select("Acordo.:Não há acordo.") == 2) {
			mes "[Izaac]";
			mes "Hã~";
			mes "Tudo bem.";
			mes "Embora sejam";
			mes .@item$ + "mais útil";
			mes "para um aventureiro como você?";
			close;
		}
		if (checkweight(501,.@amount) == 0) { //custom check
			mes "[Izaac]";
			mes "Você está acima do peso.";
			close;
		}
		delitem .@item,.@trade_amount;
		getitem 501,.@amount; //Red_Potion
		mes "[Izaac]";
		mes "Ai está!";
		mes "Verifique quantos"+.@item$;
		mes "Eu te dei, deve ser bom.";
		mes "Obrigado, foi um bom negócio~";
		close;
	case 2:
		mes "[Izaac]";
		mes "Um roubo...?";
		mes "Se você verificar o valor de mercado";
		mes "dos itens sendo negociados, na verdade sou eu quem recebe";
		mes "roubado aqui.";
		close;
	case 3:
		mes "[Izaac]";
		mes "Bem não há problema.";
		mes "Mas volte para mim se mudar de ideia.";
		close;
	}
}

// Safe to 7 Certificate Exchanger
//============================================================
moc_para01,50,39,4	script	Eve Natalia	882,{
	mes "[Eva Natália]";
	mes "Então, estou na cidade por um tempo para ver se consigo fazer algum comércio com caçadores de MVP!";
	mes "Estou disposto a pegar alguns equipamentos MVP e dar a você estes certificados de atualização que tenho em troca;";
	mes "se tudo correr bem, abrirei novas oportunidades de negócios!";
	next;
	mes "[Eva Natália]";
	mes "Então, você tem algum dos equipamentos que estou procurando para obter alguns certificados aleatórios do Safe to 7?";
	next;
	switch(select("Que equipamento?: Sim, estou!: Não importa.")) {
	case 1:
		mes "[Eva Natália]";
		mes "Traje Sombrio de Nidhoggur 4 Aleatório Seguro para 7 Certificados";
		mes "Valkyrja's Shield 4 Random Safe para 7 Certificados";
		mes "Valkyrian Armor 5 Random Safe para 7 Certificados";
		mes "Diabolus Robe 5 aleatório seguro para 7 certificados";
		mes "Diabolus Armor 2 Random Safe para 7 Certificados";
		mes "Diabolus Boots 2 Random Safe para 7 Certificados";
		mes "Diabolus Manteau 4 aleatório seguro para 7 certificados";
		mes "Twin Edge of Naght Sieger Blue 2 Random Safe para 7 Certificados";
		mes "Twin Edge of Naght Sieger Red 2 Random Safe para 7 certificados";
		close;
	case 2:
		mes "[Eva Natália]";
		mes "Vou perguntar qual equipamento você deseja trocar. Por favor, verifique se o primeiro em seu inventário é aquele que você deseja desistir.";
		mes "^CC0000Portanto, se for cardado, atualizado, encantado, etc., você deve armazená-lo antes de continuarmos.^000000";
		next;
		set .@i, select("Espere um minuto: Traje Sombrio de Nidhoggur 4: Escudo de Valkyrja 4: Armadura Valkyriana 5: Robe Diabolus 5: Armadura Diabolus 2: Botas Diabolus 2: Diabolus Manteau 4: Borda Gêmea de Naght Sieger Azul 2: Borda Gêmea de Naght Sieger Vermelho 2")-2;
		if (.@i == -1)
			break;

		setarray .@items[0],   2554, 2115, 2357, 2374, 2375, 2433, 2537, 13412, 13413;
		setarray .@tickets[0],    4,    4,    5,    5,    2,    2,    4,     2,     2;

		set .@item, .@items[.@i];
		set .@amount, .@tickets[.@i];

		mes "[Eva Natália]";
		mes "Eu vejo que você tem"+countitem(.@item)+""+getitemname(.@item)+".";
		mes "Então você quer negociar o primeiro em seu inventário por ^00CC00"+.@amount+"Aleatório seguro para 7 certificados ^ 000000?";
		mes "Diga-me '1' se quiser ou '0' para cancelar.";
		next;
		input .@input;
		if (.@input < 0 || .@input > 1) {
			mes "[Eva Natália]";
			mes "É 0 ou 1, não pode ser tão difícil.";
			close;
		} else if (.@input == 0) {
			mes "[Eva Natália]";
			mes "É melhor ter certeza antes de negociar, tenha um bom dia.";
			close;
		}
		if (countitem(.@item) == 0) {
			mes "[Eva Natália]";
			mes "Você não tem nem 1... pare de perder meu tempo.";
			close;
		}
		mes "[Eva Natália]";
		mes "Ah, perfeito, você obtém ^CC0000" + .@amount + "^000000 Seguro para 7 certificados!";
		delitem .@item,1;
		for(set .@i,0; .@i<.@amount; set .@i,.@i+1) {
			// Note: iRO lists item 6235 as "Certificado de segurança para 7 capacetes", but it's Guarantee_Armor_6Up in our database.
			if (rand(2))
				getitem 6230,1; //Guarantee_Weapon_7Up
			else
				getitem 6234,1; //Guarantee_Armor_7Up
		}
		close;
	case 3:
		break;
	}
	mes "[Eva Natália]";
	mes "É justo, se você tiver negócios comigo no futuro, não seja tímido!";
	close;
}

// Bubble Gum & Battle Manual Exchanger
//============================================================
moc_para01,48,184,2	script	Gum & Manual Exchanger	562,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "Você está carregando muitos itens agora.";
		close;	
	}
	mes "[Máquina Comercial]";
	mes "Insira chicletes e/ou manuais de batalha para trocar por chicletes e manuais de maior eficiência.";
	next;
	mes "[Máquina Comercial]";
	mes "Taxa de câmbio:";
	mes "2 chicletes -> 1 chiclete HE";
	mes "2 Manual de Batalha -> 1 Manual de Batalha HE";
	mes "4 Manual de Batalha HE -> 1 Manual de Batalha X3";
	mes "3 Manual de Batalha Grosso -> 2 Manual de Batalha X3";
	mes "Pelo que você gostaria de trocar?";
	next;
	switch(select("HE Bubble Gum:HE Manual de Batalha:1 Manual de Batalha X3:2 Manual de Batalha X3:1 Costume Corsair:Nada")) {
	case 1:
		callsub L_Exchange,12210,2,12412,1;
		break;
	case 2:
		callsub L_Exchange,12208,2,12411,1;
		break;
	case 3:
		callsub L_Exchange,12411,4,14545,1;
		break;
	case 4:
		callsub L_Exchange,12312,3,14545,2;
		break;
	case 5:
		mes "[Máquina Comercial]";
		mes "Posso te dar 1 Battle Manual X3 por 1 Costume Corsair.";
		next;
		if (countitem(19619) == 0) {
			mes "[Máquina Comercial]";
			mes "Você não tem um chapéu Costume Corsair para fazer essa troca.";
			close;
		}
		next;
		if(select("Sim, dê para mim!:Não, espere ainda não.") == 2)
			close;
		// Unofficial dialogue.
		mes "[Máquina Comercial]";
		mes "Você coloca os itens necessários na máquina...";
		next;
		mes "[Máquina Comercial]";
		mes "A Máquina lhe deu um item em troca!";
		delitem 19619,1; //C_Corsair
		getitem 14545,1; //Battle_Manual_X3
		close;
	case 6:
		close;
	}

//callsub L_Exchange,<Insert Item>,<Amount>,<Return Item>,<Amount>
L_Exchange:
	mes "[Máquina Comercial]";
	mes "Quantos"+getitemname(getarg(2))+"você quer?";
	mes "Posso te dar até 60.";
	mes "Digite 0 para cancelar.";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Máquina Comercial]";
		mes "Cancelado.";
		close;
	} else if (.@amount < 0 || .@amount > 60) {
		mes "[Máquina Comercial]";
		mes "Eu disse apenas 60 no máximo.";
		close;
	}
	mes "[Máquina Comercial]";
	mes "Isso é um total de"+.@amount+""+getitemname(getarg(2))+".";
	mes "Isso está correto?";
	next;
	if(select("Sim, entregue-os para mim!:Não, espere ainda.") == 2)
		close;
	set .@cost, getarg(1) * .@amount;
	set .@total_amount, getarg(3) * .@amount;
	if (countitem(getarg(0)) < .@cost) {
		mes "[Máquina Comercial]";
		mes "Você não tem o suficiente"+getitemname(getarg(0))+"para fazer esse comércio.";
		close;
	}
	delitem getarg(0),.@cost;
	getitem getarg(2),.@total_amount;
	close;
}

// Merry Badger
//============================================================
moc_para01,13,22,4	script	Merry Badger#xch	882,{
	disable_items;
	mes "[Texugo Alegre]";
	mes "Olá!  ";
	next;
	mes "[Texugo Alegre]";
	mes "Posso pegar seus distintivos de mérito e lhe dar algumas coisas úteis, ou posso pegar algumas de suas coisas e lhe dar alguns distintivos de mérito do Éden.";
	mes "À medida que obtiver mais autorização, poderei oferecer mais coisas para negociar com você, então lembre-se definitivamente de mim ao visitar o Grupo Eden.";
	next;
	// switch( select( "Resgatar emblemas", "Troque emblemas FOR", "Taxas de câmbio", "15 Manuais de Trabalho - 1 Caixa JM", "Ingressos para a Fortaleza do Céu", "Especial Semanal" ) ) {// Sky Fortress Tickets meaningless without the instance
	switch( select( "Resgatar emblemas", "Troque emblemas FOR", "Taxas de câmbio", "15 Manuais de Trabalho - 1 Caixa JM", "", "Especial Semanal" ) ) {
	case 1:
		setarray .@menu_list$[0],
			"4 EMB para crachá de 10 KVM",
			"4 EMB por 10 medalhas de bravura",
			"4 EMB por 10 Medalhas de Valor", 
			"8 EMB para 1 pedra de reinicialização",
			"5 EMB para Buff do Manual de Batalha",
			"5 EMB para Buff Manual de Batalha de Trabalho",
			"10 EMB para Buff do Manual de Batalha HE",
			"20 EMB para 3X Buff Manual de Batalha",
			"5 EMB para Bubblegum Buff", 
			"10 EMB para HE Bubblegum Buff",
			"",	// "5 EMB para Buff Milagroso" (item doesn't exist in db)
			"1 EMB para folha de Yggdrasil",
			"",	// "5 EMB para Ala de Aluguel de Belzebu" (item doesn't exist in db)
			"5 EMB para aluguel de cabresto",
			"5 EMB por 20 ervas azuis",
			"Símbolo de Siegfried",
			"Nada agora";
		.@menu$ = implode( .@menu_list$, ":" );
		while(1) {
			mes "[Texugo Alegre]";
			mes "Claro, tenho várias opções atualmente para seus emblemas!";
			next;
			switch( select(.@menu$) ) {
			case 1:// "4 EMB para crachá de 10 KVM"
				callsub L_Exchange,4,6376,10;// KVM_Badge
				break;
			case 2:// "4 EMB por 10 medalhas de bravura"
				callsub L_Exchange,4,7828,10;// BF_Badge1
				break;
			case 3:// "4 EMB por 10 Medalhas de Valor"
				callsub L_Exchange,4,7829,10;// BF_Badge2
				break;
			case 4:// "8 EMB para 1 pedra de reinicialização"
				callsub L_Exchange,8,6320,1;// Premium_Reset_Stone
				break;
			case 5:// "5 EMB para Buff do Manual de Batalha"
				callsub L_Exchange,5,12208,1;// Battle_Manual
				break;
			case 6:// "5 EMB para Buff Manual de Batalha de Trabalho"
				callsub L_Exchange,5,14592,1;// Job_Manual50
				break;
			case 7:// "10 EMB para Buff do Manual de Batalha HE"
				callsub L_Exchange,10,12411,1;// HE_Battle_Manual
				break;
			case 8:// "20 EMB para 3X Buff Manual de Batalha"
				callsub L_Exchange,20,14545,1;// Battle_Manual_X3
				break;
			case 9:// "5 EMB para Bubblegum Buff"
				callsub L_Exchange,5,12210,1;// Bubble_Gum
				break;
			case 10:// "10 EMB para HE Bubblegum Buff"
				callsub L_Exchange,10,12412,1;// HE_Bubble_Gum
				break;
			case 11:// "5 EMB para Buff Milagroso"
				.@item_id = 22819;
				if (countitem(6495) < 5) {
					mes "[Texugo Alegre]";
					mes "Você não tem ^00CC00Insígnias de Mérito Eden^000000 suficientes. ";
					break;
				}
				mes "[Texugo Alegre]";
				mes "Ok, posso te dar ^ff0000" + getitemname(.@item_id) + "^000000 por ^0066CC5 do(s) seu(s) Distintivo(s) de Mérito do Éden^000000.";
				mes "Você quer?";
				next;
				if (select("Sim", "Não") == 2) {
					mes "[Texugo Alegre]";
					mes "Entendo, vamos ver se você gostaria de algo diferente!";
					break;
				}
				mes "[Texugo Alegre]";
				mes "Ai está! ";
				delitem 6495, 5;// Para_Logro_Badge
				consumeitem .@item_id;
				break;
			case 12:// "1 EMB para folha de Yggdrasil"
				callsub L_Exchange,1,610,1;// Leaf_Of_Yggdrasil
				break;
			case 13:// "5 EMB para Ala de Aluguel de Belzebu"
				.@item_id = 14645;
				if (countitem(6495) < 5) {
					mes "[Texugo Alegre]";
					mes "Você não tem ^00CC00Insígnias de Mérito Eden^000000 suficientes. ";
					break;
				}
				mes "[Texugo Alegre]";
				mes "Ok, posso te dar ^ff0000" + getitemname(.@item_id) + "^000000 por ^0066CC5 do(s) seu(s) Distintivo(s) de Mérito do Éden^000000.";
				mes "Você quer?";
				next;
				if (select("Sim", "Não") == 2) {
					mes "[Texugo Alegre]";
					mes "Entendo, vamos ver se você gostaria de algo diferente!";
					break;
				}
				mes "[Texugo Alegre]";
				mes "Ai está! ";
				delitem 6495, 5;// Para_Logro_Badge
				rentitem .@item_id, 604800;	// 7 days
				break;
			case 14:// "5 EMB para aluguel de cabresto"
				callsub L_Exchange,5,17162,1;// Boarding_Halter_Box7
				break;
			case 15:// "5 EMB por 20 ervas azuis"
				callsub L_Exchange,5,510,20;// Blue_Herb
				break;
			case 16:// Token of Siegfried
				if (vip_status(VIP_STATUS_ACTIVE) == false) {
					mes "[Texugo Alegre]";
					mes "VIP Services oferece isso a qualquer usuário com VIP !!";
					close;
				}
				// custom from here
				.@date = gettime(DT_YYYYMMDD);
				if (.@date <= eden_iro_token_date) {
					mes "[Texugo Alegre]";
					mes "Você já recebeu seu Token hoje!";
					close;
				}
				mes "[Texugo Alegre]";
				mes "Ai está! ";
				eden_iro_token_date = .@date;
				getitem 7621,10;// Token_Of_Siegfried
				close;
			case 17:// Nothing Now
				mes "[Texugo Alegre]";
				mes "Maravilhoso!";
				mes "Estarei aqui se você quiser trocar por Distintivos de Mérito do Éden!";
				close;
			}
			next;
		}
	case 2:
		mes "[Texugo Alegre]";
		mes "Tenho uma seleção limitada no momento, mas aqui está o que posso oferecer para ganhar 1 Distintivo de Mérito do Éden!";
		next;
		switch( select( "Itens etc.", "Equipamentos", "Cancelar" ) ) {
		case 1:
			while(1) {
				if (select( "5x Caixa Roxa Velha", "Nada" ) == 2) {
					mes "[Texugo Alegre]";
					mes "Maravilhoso!";
					mes "Estarei aqui se você quiser trocar por Distintivos de Mérito do Éden!";
					close;
				}
				while(1) {
					if (countitem(617) < 5) {
						mes "[Texugo Alegre]";
						mes "Você não tem o suficiente para obter 1 Distintivo de Mérito do Éden. ";
						break;
					}
					mes "[Texugo Alegre]";
					mes "Ok, posso lhe dar ^CC00001 Eden Merit Badge^000000 por ^0000CC5 de suas Old Purple Boxes^000000. ";
					next;
					if (select( "Sim", "Não" ) == 2) {
						mes "[Texugo Alegre]";
						mes "Eu entendo, vamos ver se você gostaria de algo diferente!";
						break;
					}
					else {
						mes "[Texugo Alegre]";
						mes "Ai está!  ";
						delitem 617,5;// Old_Violet_Box
						getitem 6495,1;// Para_Logro_Badge
						next;
					}
				}
				next;
			}
		case 2:
			while(1) {
				mes "[Texugo Alegre]";
				mes "Vou pegar o item principal do seu inventário! ";
				next;
				switch( select( "Pele de cobra gigante faceworm", "Pele de cobra gigante Faceworm [1]", "Nada" ) ) {
				case 1:
					.@item_req = 20717;// FaceWorm_Skin
					.@item_name$ = "Pele de cobra gigante faceworm";
					.@amount_reward = 5;
					break;
				case 2:
					.@item_req = 20718;// FaceWorm_Skin_
					.@item_name$ = "Pele de cobra gigante Faceworm [1]";
					.@amount_reward = 7;
					break;
				case 3:
					mes "[Texugo Alegre]";
					mes "Maravilhoso!";
					mes "Estarei aqui se você quiser trocar por Distintivos de Mérito do Éden!";
					next;
					mes "[Texugo Alegre]";
					mes "Bem, tudo bem, se você quiser ver o que ofereço é só pedir!";
					close;
				}
				while(1) {
					if (countitem(.@item_req) < 1) {
						mes "[Texugo Alegre]";
						mes "Você não tem o suficiente para obter 1 Distintivo de Mérito do Éden. ";
						break;
					}
					mes "[Texugo Alegre]";
					mes "Ok, posso te dar ^ff0000" + .@amount_reward + "Distintivo de Mérito Éden^000000 por ^00ff00" + .@item_name$ + "^000000. ";
					next;
					if (select( "Sim", "Não" ) == 2) {
						mes "[Texugo Alegre]";
						mes "Eu entendo, vamos ver se você gostaria de algo diferente!";
						break;
					}
					delitem .@item_req, 1;
					getitem 6495, .@amount_reward;// Para_Logro_Badge
					mes "[Texugo Alegre]";
					mes "Ai está! ";
					next;
				}
				next;
			}
		case 3:
			mes "[Texugo Alegre]";
			mes "Bem, tudo bem, se você quiser ver o que ofereço é só pedir!";
			close;
		}
	case 3:
		mes "[Texugo Alegre]";
		mes "Isto é o que eu darei a você pelos seus ^00CC00Distintivos de Mérito Eden^000000!";
		mes "Ok, tenho um especial para 3 Distintivos de Mérito. ";
		mes "^00CC002 Distintivos de Mérito Eden^000000 = ^0000CC10x Distintivo KVM^000000";
		mes "^00CC002 Distintivos de Mérito do Éden^000000 = ^0000CC10x Medalhas de Bravura^000000";
		mes "^00CC002 Distintivos de Mérito do Éden^000000 = ^0000CC10x Medalhas de Valor^000000";
		mes "^00CC005 Distintivos de Mérito do Éden^000000 = ^0000CC5x Poção de Vida Média^000000";
		mes "^00CC005 Distintivos de Mérito do Éden^000000 = ^0000CC5x Seguro de Vida^000000";
		mes "^00CC005 Distintivos de Mérito do Éden^000000 = ^0000CC5x Token de Siegfried^000000";
		mes "^00CC007 Distintivos de Mérito do Éden^000000 = ^0000CC1x Manual de Batalha de Trabalho^000000";
		mes "^00CC008 Distintivos de Mérito do Éden^000000 = ^0000CC1x Redefinir Pedra^000000";
		next;
		mes "[Texugo Alegre]";
		mes "Isto é o que você pode me dar para conseguir ^00CC001 Distintivo de Mérito do Éden^000000!";
		mes "^0000CC5x Caixa Roxa Antiga^000000";
		close;
	case 4:
		mes "[Texugo Alegre]";
		mes "Eu estava fortemente armado para dar isso, mas você vale a pena!";
		mes "Você deseja trocar 15 Manuais de Trabalho para obter 1 caixa negociável de 10 Manuais de Trabalho?";
		mes "Eu sei que 15 não é igual a 10, mas as caixas são caras, você sabe.";
		next;
		if (countitem(14592) < 15) {
			mes "[Texugo Alegre]";
			mes "Você não tem 15 ou mais manuais de trabalho para obter 1 caixa!";
			close;
		}
		// custom dialogue below
		if (select("Sim", "Não") == 2) {
			mes "[Texugo Alegre]";
			mes "Maravilhoso!";
			mes "Estarei aqui se você quiser trocar por Distintivos de Mérito do Éden!";
			close;
		}
		mes "[Texugo Alegre]";
		mes "Maravilhoso, o que mais você quer?";
		delitem 14592,15; //Job_Manual50
		getitem 13990,1; //Job_Manual50_Box
		close;
	case 5:
		mes "[Texugo Alegre]";
		mes "Eu realmente não entendo por que, mas tenho uma pilha de cupons para vender.";
		mes "Você quer trocar 25 Distintivos de Mérito do Éden por 1 ingresso de masmorra? ";
		next;
		if (select( "Sim", "Não" ) == 2) {
			mes "[Texugo Alegre]";
			mes "Eu entendo, vamos ver se você gostaria de algo diferente!";
			close;
		}
		if (countitem(6495) < 25) {
			mes "[Texugo Alegre]";
			mes "Você não tem emblemas suficientes!";
			close;
		}
		delitem 6495,25;
		if (vip_status(VIP_STATUS_ACTIVE))
			getitem 17570,1;// Sky Fortress Ticket 3 Hour Box
		else
			getitem 17569,1;// Sky Fortress Ticket 1 Hour Box
		mes "[Texugo Alegre]";
		mes "Ai está! ";
		close;
	case 6:
		while(1) {
			mes "[Texugo Alegre]";
			mes "O especial semanal é ....";
			next;
			mes "[Texugo Alegre]";
			mes "Ok, tenho um especial para 3 Distintivos de Mérito ou 1.000.000 zeny.  ";
			mes "^00CC0010 Fruta Espinhosa^000000, ^0000CC1 Vaso de Mandrágora^000000 e ^00CC003 Ramo de Ancião^000000.";
			next;
			switch( select( "Distintivos", "Zeny", "Não" ) ) {
			case 1:
				if (countitem(6495) < 3) {
					mes "[Texugo Alegre]";
					mes "Você não tem ^00CC00Insígnias de Mérito Eden^000000 suficientes. ";
					next;
					continue;
				}
				delitem 6495,3; //Para_Logro_Badge
				break;
			case 2:
				if (Zeny < 1000000) {
					mes "[Texugo Alegre]";
					mes "Você não tem ^00CC00Zeny^000000 suficientes. ";
					next;
					continue;
				}
				Zeny -= 1000000;
				break;
			case 3:
				mes "[Texugo Alegre]";
				mes "Eu entendo, vamos ver se você gostaria de algo diferente!";
				close;
			}
			getitem 7938,40;// Light_Granule
			getitem 6244,40;// Gun_Power
			getitem 709,3;	// Izidor
			getitem 576,10;	// Prickly_Fruit
			getitem 6217,1;	// Mandragora_Flowerpot
			getitem 7939,3;	// Elder_Branch
			mes "[Texugo Alegre]";
			mes "Ai está! ";
			next;
		}
	}
	end;

//callsub L_Exchange,<badge cost>,<item id>,<amount>;
L_Exchange:
	if (countitem(6495) < getarg(0)) {
		mes "[Texugo Alegre]";
		mes "Você não tem ^00CC00Insígnias de Mérito Eden^000000 suficientes. ";
		return;
	}
	mes "[Texugo Alegre]";
	mes sprintf("Ok, posso lhe dar ^ff0000%s^000000 por ^0066CC%d do(s) seu(s) Distintivo(s) de Mérito Eden^000000.",
				callfunc("F_InserirPlural",getarg(2),getitemname(getarg(1))), getarg(0));
	mes "Você quer?";
	next;
	if (select("Sim", "Não") == 2) {
		mes "[Texugo Alegre]";
		mes "Entendo, vamos ver se você gostaria de algo diferente!";
		return;
	}
	mes "[Texugo Alegre]";
	mes "Ai está! ";
	delitem 6495, getarg(0); //Para_Logro_Badge
	getitem getarg(1),getarg(2);
	return;
}

// Kafra Stacker
//============================================================
moc_para01,41,169,2	script	Kafra Stacker#eden	749,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "- Espere um momento! ";
		mes "- Atualmente você está carregando -";
		mes "- muito peso nos itens. ";
		mes "- Por favor, volte mais tarde -";
		mes "- depois de colocar alguns no armazenamento Kafra. ";
		close;
	}
	mes "[A MA]";
	mes "Posso levar seus consumíveis Kafra não empilháveis";
	mes "e faça-os caber perfeitamente em pilhas!";
	next;
	mes "[A MA]";
	mes "Por favor, entenda que eu posso";
	mes "empilhe apenas 10 grupos por vez";
	mes "então continue falando comigo até";
	mes "você tem 1 pilha de cada!";
	next;
	while(1) {
		if(select("Faça isso!: Não importa.") == 2)
			break;
		// Something strange goes on here, haven't quite figured it out...
		mes "[A MA]";
		mes "Dez pilhas para baixo!";
		mes "Quer fazer outro?";
		next;
	}
	mes "[A MA]";
	mes "Ok então, mais tarde.";
	close;
}

// Code Redeemer
//============================================================
moc_para01,38,162,2	script	Code the Redeemer#Give1	405,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2000) {
		mes "^0000ffEspere aí!";
		mes "Você está carregando muitos itens e não tem espaço suficiente no inventário para receber recompensas. ";
		close;
	}
	mes "[Codifique o Redentor]";
	mes "Bem-vindo,"+strcharinfo(0)+"!";
	mes "Eu estava esperando por você.";
	next;
	mes "[Codifique o Redentor]";
	mes "Sou distribuidor de todos os";
	mes "itens que os aventureiros gostam";
	mes "você mesmo compra em sites";
	mes "ou receba como promoções.";
	next;
	mes "[Codifique o Redentor]";
	mes "^0000FFSe a lista estiver vazia,";
	mes "isso significa que você tem";
	mes "não comprado ou inserido";
	mes "seu código de série do site de compras.^000000";
	next;

	// If you have a code redemption system, write your SQL queries here.
	select("");

	close;
}

// Cash Shops
//============================================================
/* Due to the irregularity of iRO's databases, some of these sprites may not display properly. */

moc_para01,32,171,6	cashshop	Kafra Headgears#eden1	115,14024:500,14098:150,17081:300,14097:500,14094:1000,14017:400,14099:300
moc_para01,35,174,2	cashshop	Kafra Dye Shop#dyes2	114,6287:150
moc_para01,37,170,2	cashshop	Kafra Consumables#eden3	112,13810:5,14167:400,16556:100,14111:75,14075:25,16419:30,14107:70,14104:50,14112:25,14082:30,14091:60,14088:30,14085:60,14169:40,14170:40,14172:40,14173:40,14171:40,14174:40,14159:100,14160:100,14161:100,14162:100,14163:100:14164:100,16420:1500
moc_para01,32,174,2	cashshop	Kafra Rental Items#eden	721,16683:150,16682:50,14126:250,14125:250,16430:250,14137:250,14127:250,14118:250,14119:250,14132:250,16425:250,14124:250,14133:250,14130:250,16424:250,14136:250,14122:250,16426:250,14135:250,14134:250,14138:250,16428:250,16431:250,14139:250,14131:250,16429:250,16427:250
moc_para01,37,172,2	cashshop	Kafra Costume#eden5	860,16974:300,16972:600,16973:150,16975:150,16977:150,17014:150,17015:150
moc_para01,35,168,1	cashshop	Kafra Utility Shop#eden	113,16677:25,13990:400,16396:100,16394:100,17106:500,6241:10,6240:10,6225:15,6226:15,14002:5,6294:500,7776:50,7619:30,7620:30,12208:40,14156:400,12210:40,14158:400,14042:50,14044:50,14046:50,14048:50,14114:50,14166:40,16418:500,16381:400,14165:100,14157:100,14178:100

// Duplicate NPCs
//============================================================
/* Technically these NPC names are different, but it's not worth editing the original scripts. */

// Weapon/Armor Refiner "Normalson" (merchants/refine.txt)
moc_para01,14,185,4	script	Normalson#ed	826,{
	callfunc "refinarmain","Normalson",0;
	end;
}

// Advanced Refiner "Suhnmi" (merchants/advanced_refiner.txt)
moc_para01,26,185,4	duplicate(Suhnbi#cash)	Suhnmi#eden	85

// HD Refiner "Martelo poderoso do ferreiro" (re/merchants/hd_refiner.txt)
// A combination of the +7~9 and +10 and up refiner, currently a placeholder.
moc_para01,38,185,4	duplicate(MightyHammer)	Mighty Hammer#ed	826

// Refiner Effect
-	script	#eden_refine_effect	-1,{
OnInit:
	initnpctimer;
	end;
OnTimer5000:
	specialeffect EF_REFINEOK,AREA,"Suhnmi";
	specialeffect EF_REFINEOK,AREA,"Martelo Poderoso";
	initnpctimer;
	end;
}

// Gym Pass Trainer "Cabus rasgado" (other/gympass)
moc_para01,33,162,6	duplicate(Ripped Cabus#GymPass)	Ripped Callus#ed	899

// RWC 2012 Enchanter "Ladrão de Ouro" (events/RWC_2012.txt)
// A combination of the slotter and enchanter, currently a placeholder.
moc_para01,27,179,4	script	RWC Enchanter#new10	87,{ end; }

// Cash Headgear Dyer "Álora" (merchants/cashheadgear_dye.txt)
// Also converts some +8 and up headgears into costumes, currently a placeholder.
moc_para01,21,185,4	duplicate(Alora)	Alora#headgear_dye2	862

// Stylist "Dinorah Lacostt" (?)
// Dyes clothing for an Omni Clothing Dye, currently a placeholder.
moc_para01,33,185,4	script	Dinorah Lacostt#ed	862,{ end; }

// Commonly Updated NPCs
//============================================================
/* These scripts change on a regular basis, so they're mostly just placeholders for now. */

moc_para01,29,35,4	script	Gramps#huntquests	866,{
	mes "[Vovô]";
	mes "Quando você começar a ser meu";
	mes "idade, você fica amargo.";
	mes "Há muito tempo que não faço nada sobre os monstros que vagam por aí";
	mes "Midgard.";
	next;
	mes "[Vovô]";
	mes "É por isso que estou pedindo ajuda a vocês, wippersnappers";
	mes "eu caço alguns monstros.";
	mes "Você vai me ajudar, jovem aventureiro?";
	close;
	//next;
	//switch(select("Sim, vou ajudar.:Me recompense!:^ff0000Quero parar de caçar^000000:^0000ffVocê pode me teleportar?^000000:Você pode apagar o cronômetro?:Que missões você está dando?:Dê-me VIP limitado de 3 dias ")) {
}

moc_para01,174,33,3	script	Bathory#2012spirits	1102,{
	mes "[Bathory]";
	mes "Eheeheehee! ";
	next;
	mes "[Bathory]";
	mes "Que tal isso? ";
	next;
	switch(select("Prepare-se para a decolagem!: Não, obrigado.")) {
	case 1:
		if (Zeny < 1000) {
			mes "[Bathory]";
			mes "Vamos, querido...";
			mes "Você nem tem 1.000 zeny?";
			close;
		}
		mes "[Bathory]";
		mes "Tudo bem, segure firme!";
		close2;
		set Zeny, Zeny-1000;
		warp "niflheim",194,185;
		end;
	case 2:
		mes "["+strcharinfo(0)+"]";
		mes "Não, obrigado, talvez mais tarde.";
		close;
	}
}

moc_para01,26,174,4	script	Grandma Boxter#boxx	840,{ end; }

// Mail Annex Station
//============================================================
/* Map "auction_03" is a duplicate of "auction_02".

moc_para01,30,187,4	script	Mail Annex Station	888,{
	mes "[Estação Anexo de Correio]";
	mes "Gostaria de ir para a Estação Anexo Kafra Mail?";
	next;
	switch(select("Sim, envie-me agora!:Não, obrigado.")) {
	case 1:
		mes "[Estação Anexo de Correio]";
		mes "Enviando você para a Sala Anexo de Correio.";
		close2;
		warp "leilão_03",151,23;
		end;
	case 2:
		mes "[Estação Anexo de Correio]";
		mes "OK então!";
		close;
	}
}
auction_03,152,16,0	warp	#mail_annex_warp	1,1,moc_para01,30,184

auction_03,152,42,4	script	Kafra Employee#annex	117,{
	mes "[Funcionário da Kafra]";
	mes "Bem-vindo à Estação Anexo do Correio de Kafra. ";
	next;
	mes "[Funcionário da Kafra]";
	mes "O que você gostaria de fazer?";
	next;
	switch(select("Acessar Correio:Armazenamento:Cancelar")) {
	case 1:
		if (Zeny < 130) {
			mes "[Funcionário da Kafra]";
			mes "Sinto muito, mas você não tem dinheiro suficiente.";
			mes "Para usar o serviço de caixa postal,";
			mes "você é obrigado a pagar 130 zeny.";
			close;
		}
		mes "[Funcionário da Kafra]";
		mes "Obrigado por usar o serviço Kafra.";
		set Zeny, Zeny - 130;
		close2;
		openmail;
		end;
	case 2:
		callfunc "F_KafStor",0,1200,0;
		end;
	case 3:
		mes "[Funcionário da Kafra]";
		mes "Obrigado por usar o serviço Kafra.";
		close;
	}
}

auction_03,145,47,2	shop	Tool Dealer#annex	83,611:-1,501:-1,502:-1,503:-1,506:-1,504:-1,645:-1,656:-1,657:-1,601:-1,602:-1,1065:-1,911:-1,910:-1,717:-1,1092:-1,1093:-1,713:400

*/

// Fallen_Angel_Wing Enchant
//============================================================
moc_para01,179,27,3	script	Dark Luhir#new10	4_F_VALKYRIE2,{
	disable_items;
	mes "[Luhir Sombrio]";
	mes "Olá ~ Estou distribuindo estatísticas extras para aqueles que possuem um item Fallen Angel Wing.";
	next;
	if (getequipid(EQI_GARMENT) != 2589) {// Fallen_Angel_Wing
		mes "[Luhir Sombrio]";
		mes "Por favor ^0000vista seu item Fallen Angel Wing primeiro^000000, e então eu lhe darei estatísticas extras.";
		close;
	}
	switch( select( "Cancelar", "Encantar Asa de Anjo Caído", "Redefinir estatísticas da asa do anjo caído" ) ) {
	case 1:
		mes "[Luhir Sombrio]";
		mes "Mais tarde então...";
		close;
	case 2:
		.@refine = getequiprefinerycnt(EQI_GARMENT);
		setarray .@card[0],
			getequipcardid(EQI_GARMENT,0),
			getequipcardid(EQI_GARMENT,1),
			getequipcardid(EQI_GARMENT,2),
			getequipcardid(EQI_GARMENT,3);
		mes "[Luhir Sombrio]";
		mes "Apenas 1 soquete pode obter estatísticas extras quando o nível de refinamento estiver entre 1 e 6.";
		mes "Se o nível de refinamento for 7~8, ele poderá receber estatísticas para 2 soquetes.";
		mes "9 pode receber estatísticas para todos os 3 soquetes.";
		next;
		mes "[Luhir Sombrio]";
		if (.@refine < 7)
			mes "O nível de refinamento deste item é inferior a 7. Somente ^ff0000socket slot 4^000000 pode receber estatísticas extras.";
		else if (.@refine < 9)
			mes "O nível de refinamento deste item está entre 7~8. ";
		else
			mes "O nível de refinamento deste item é superior a 9. Todos os 3 ^ff0000Slots de soquete 2, 3 e 4^000000 podem receber estatísticas extras.";
		mes "Que tipo de estatísticas extras você deseja?";
		next;
		switch( select( "Deixe-me colocar meu equipamento", "1. Espírito de luta", "2. Ataque Mágico", "3.Tiro com arco", "4. Crítico", "5. HP máximo", "6.MaxSP", "7. Velocidade de ataque", "8.STR", "9.AGI", "10.VIT", "11.INT", "12.DEX", "13.LUK" ) ) {
		case 1:
			mes "[Luhir Sombrio]";
			mes "Por favor, volte depois de equipar seu item.";
			close;
		case 2:
			setarray .@enchants[0],4809,4808,4820,4821;		// Fighting Spirit 3~5 / Fighting Spirit 6
			.@string$ = "As estatísticas do Fighting Spirit serão fornecidas aleatoriamente na faixa de 3 a 5 (ATK HIT).";
			break;
		case 3:
			setarray .@enchants[0],4812,4826,4827,4828;		// Spell 4~6 / Spell 7
			.@string$ = "As estatísticas do feitiço mágico serão fornecidas aleatoriamente de 4 a 6 (MATK Reduz o tempo de lançamento da mudança).";
			break;
		case 4:
			setarray .@enchants[0],4832,4833,4834,4835;		// Expert Archer 1~3 / Expert Archer 4
			.@string$ = "As estatísticas do tiro com arco serão fornecidas aleatoriamente de 1 a 3 (taxa de ataque ao alcance).";
			break;
		case 5:
			setarray .@enchants[0],4863,4864,4865,4866;		// Fatal 1~3 / Fatal 4
			.@string$ = "As estatísticas críticas serão fornecidas aleatoriamente de 1 a 4 (CRI de dano crítico).";
			break;
		case 6:
			setarray .@enchants[0],4861,4862,4867,4868;		// MaxHP +1~3% / MaxHP +4%
			.@string$ = "As estatísticas de MHP serão fornecidas aleatoriamente de 1 a 3 (aumento de porcentagem de MHP).";
			break;
		case 7:
			setarray .@enchants[0],4870,4800,4871,4801;		// +25,50 or 75 sp / +100 sp
			.@string$ = "As estatísticas de SP serão fornecidas aleatoriamente de 25 a 75.";
			break;
		case 8:
			setarray .@enchants[0],4869,4872,4873,4807;		// Delayafterattack 1~3 / +1 ASPD
			.@string$ = "As estatísticas de atraso de ataque serão fornecidas aleatoriamente de 1 a 3 (reduz a porcentagem de atraso de ataque).";
			break;
		case 9:
			setarray .@enchants[0],4702,4703,4704,4853;		// Str +3~5 / Special Str
			.@string$ = "As estatísticas de STR serão fornecidas aleatoriamente de 3 a 5.";
			break;
		case 10:
			setarray .@enchants[0],4731,4732,4733,4854;		// Agi +2~4 / Special Agi
			.@string$ = "As estatísticas AGI serão fornecidas aleatoriamente de 2 a 4.";
			break;
		case 11:
			setarray .@enchants[0],4742,4743,4744,4855;		// Vit +3~5 / Special Vit
			.@string$ = "As estatísticas VIT serão fornecidas aleatoriamente de 3 a 5.";
			break;
		case 12:
			setarray .@enchants[0],4712,4713,4714,4856;		// Int +3~5 / Special Int
			.@string$ = "As estatísticas de INT serão fornecidas aleatoriamente de 3 a 5.";
			break;
		case 13:
			setarray .@enchants[0],4722,4723,4724,4857;		// Dex +3~5 / Special Dex
			.@string$ = "As estatísticas de DEX serão fornecidas aleatoriamente de 3 a 5.";
			break;
		case 14:
			setarray .@enchants[0],4752,4753,4754,4858;		// Luk +3~5 / Special Luk
			.@string$ = "As estatísticas de LUK serão fornecidas aleatoriamente de 3 a 5.";
			break;
		}
		mes "[Luhir Sombrio]";
		mes .@string$ + "^ff0000As estatísticas fornecidas não podem ser revertidas.^000000 Deseja continuar?";
		next;
		if (select( "1. Sair", "2.Sim, quero tentar isso." ) == 1) {
			mes "[Luhir Sombrio]";
			mes "Mais tarde então...";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_GARMENT, 2589) || callfunc("F_IsEquipCardHack", EQI_GARMENT, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", EQI_GARMENT, .@refine))
			close;

		if (.@card[3] == 0) {
			.@card[3] = .@enchants[ rand(3) ];
			.@slot = 4;
		}
		else if (.@card[2] == 0 && .@refine >= 7) {
			.@card[2] = .@enchants[ rand(3) ];
			.@slot = 3;
		}
		else if (.@card[1] == 0 && .@refine >= 9) {
			.@card[1] = .@enchants[ rand(4) ];
			.@slot = 2;
		}
		else {
			mes "[Luhir Sombrio]";
			mes "Parece que este item já está com encantamento máximo. ";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[Luhir Sombrio]";
		mes "Dando capacidade ao item ^990000" + .@slot + "soquete ^ 000000.";
		delequip EQI_GARMENT;
		getitem2 2589,1,1, .@refine,0, .@card[0], .@card[1], .@card[2], .@card[3];
		close;
	case 3:
		mes "[Luhir Sombrio]";
		mes "A redefinição das estatísticas da Asa do Anjo Caído custará 1.000.000 zeny?";
		mes "Você deseja continuar?";
		next;
		if (select( "Sim", "Não, obrigado!" ) == 2)
			close;
		if (Zeny < 1000000) {
			mes "[Luhir Sombrio]";
			mes "Seu mentiroso, você não tem zeny suficiente!";
			close;
		}
		if (getequipid(EQI_GARMENT) == 2589) {
			.@refine = getequiprefinerycnt(EQI_GARMENT);
			.@card[0] = getequipcardid(EQI_GARMENT,0);

			mes "[Luhir Sombrio]";
			specialeffect2 EF_REPAIRWEAPON;
			mes "Redefinindo estatísticas de itens.";
			delequip EQI_GARMENT;
			Zeny -= 1000000;
			getitem2 2589,1,1, .@refine,0, .@card[0],0,0,0;// Fallen_Angel_Wing
		}
		close;
	}
}

