//===========================================================\\
//          ____   __   _  _  __    ____   __   ____         //
//         / ___) /  \ / )( \(  )  (  __) / _\ (_  _)        \\
//         \___ \(  O )) \/ (/ (_/\ ) _) /    \  )(          //
//         (____/ \__/ \____/\____/(____)\_/\_/ (__)         \\
//                                                           //
//================== www.souleat.com.br =====================\\
//======================= by: Guru ==========================//

// Generais
//============================================================
bat_room,161,158,3	script	Ajudante do Gen. Guillaume#01	419,{ end; }
bat_room,161,160,3	script	Ajudante do Gen. Guillaume#03	419,{ end; }

bat_room,160,141,3	script	Príncipe Croix	416,{
	cutin "bat_crua1",2;
	mes "[Príncipe Croix]";
	mes "Aventureiro sábio, por que não nos empresta sua força para a vitória?";
	next;
	switch(select("Qual é o motivo da Batalha?:Fale-me sobre o General Guillaume")) {
	case 1:
		cutin "bat_crua2",2;
		mes "[Príncipe Croix]";
		mes "O grande rei de Maroll, Marcel Marollo VII, está muito doente ultimamente.";
		mes "Sua Majestade declarou que deixará o futuro de Maroll nas mãos de mim ou do 3º príncipe, General Guillaume.";
		next;
		mes "[Príncipe Croix]";
		mes "O General Guillaume pode ter vantagem nesta batalha, pois é o grande general de Maroll, mas isso não significa automaticamente que ele vencerá.";
		mes "Eu quero ganhar esta batalha para trazer prosperidade ao povo de Maroll. Eles já sofreram o suficiente com a guerra...";
		next;
		switch(select("Sim, quero me juntar a você.:Encerrar Conversa")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Príncipe Croix]";
			mes "Muito obrigado. Sinto que posso vencer com a ajuda de aventureiros como você.";
			mes "Agora, por favor, vá para baixo e junte-se aos seus camaradas para afiar suas habilidades e lutar contra o inimigo!";
			break;
		case 2:
			mes "[Príncipe Croix]";
			mes "Por Maroll!";
			break;
		}
		break;
	case 2:
		cutin "bat_crua2",2;
		mes "[Príncipe Croix]";
		mes "O 3º Príncipe Guillaume é o grande general de Maroll.";
		mes "É perda de tempo explicar o quão grande líder ou senhor da guerra ele é, já que comanda o grande poder militar de Maroll.";
		next;
		mes "[Príncipe Croix]";
		mes "Infelizmente, há algo que ele e seus seguidores não sabem:";
		mes "Será que o povo de Maroll realmente deseja que eles gastem tanto dinheiro em poder militar?";
		mes "Nós já sofremos o suficiente com as guerras.";
		mes "Acredito que armas não são a melhor maneira de trazer prosperidade a uma nação.";
		next;
		mes "[Príncipe Croix]";
		mes "Não desejo derramar sangue, mas não tenho escolha a não ser lutar pela possibilidade de paz e pelo bem do meu povo.";
		next;
		switch(select("Sim, quero me juntar a você.:Encerrar Conversa")) {
		case 1:
			cutin "bat_crua1",2;
			mes "[Príncipe Croix]";
			mes "Muito obrigado. Sinto que posso vencer com a ajuda de aventureiros como você.";
			mes "Agora, por favor, vá para baixo e junte-se aos seus camaradas para afiar suas habilidades e lutar contra o inimigo!";
			break;
		case 2:
			mes "[Príncipe Croix]";
			mes "Por Maroll!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_crua1",255;
	cutin "bat_crua2",255;
	end;
}

bat_room,161,140,3	script	Ajudante do Príncipe Croix#01	415,{ end; }
bat_room,161,142,3	script	Ajudante do Príncipe Croix#02	415,{ end; }

bat_room,160,159,3	script	General Guillaume	420,{
	cutin "bat_kiyom2",2;
	mes "[General Guillaume]";
	mes "Aventureiro cheio de determinação, precisamos da sua habilidade para vencer esta batalha.";
	next;
	switch(select("Qual é o motivo da Batalha?:Fale-me sobre o Príncipe Croix")) {
	case 1:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "Nosso grande rei, Marcel Marollo VII, está muito doente ultimamente.";
		mes "Sua Majestade declarou que escolheu eu ou o Príncipe Croix como o próximo rei entre seus 9 filhos.";
		next;
		mes "[General Guillaume]";
		mes "Dois reis não podem compartilhar uma nação! Apenas o vencedor da batalha designada por Sua Majestade será coroado.";
		next;
		mes "[General Guillaume]";
		mes "No entanto, isso não é apenas uma batalha entre nós. Esta batalha determinará o futuro deste país.";
		mes "Eu prometo, com toda a minha honra, provar que sou aquele que pode proteger este Maroll de ameaças externas.";
		next;
		switch(select("Sim, quero me juntar a você.:Encerrar Conversa")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bem-vindo ao meu exército, camarada.";
			mes "Seus olhos me dizem que você é um soldado em quem posso confiar.";
			next;
			mes "[General Guillaume]";
			mes "Agora, vá para cima e inscreva-se na batalha com seus camaradas.";
			mes "Tenho certeza de que eles o receberão de braços abertos!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Serei aquele que capturará a bandeira!";
			break;
		}
		break;
	case 2:
		cutin "bat_kiyom1",2;
		mes "[General Guillaume]";
		mes "O 5º Príncipe Croix atualmente possui o título de Primeiro Ministro de Maroll.";
		mes "Ele acredita que todos os assuntos nacionais de uma nação podem ser discutidos e determinados em uma mesa,";
		mes "e acredita na coexistência pacífica com outros países.";
		next;
		mes "[General Guillaume]";
		mes "Ele é muito ignorante para admitir que a chamada paz é construída sobre inúmeras vidas sacrificadas em guerras, enquanto cidadãos comuns e classes altas podem viver, alheios aos horrores que permitem que vivam dessa forma.";
		next;
		mes "[General Guillaume]";
		mes "Ele é muito ingênuo para entender a realidade....";
		mes "Não posso deixar Maroll nas mãos de alguém como ele, que vive em um sonho!";
		next;
		mes "[General Guillaume]";
		mes "Suas crenças irreais afundarão este país na pobreza e tornarão o povo fraco. Se ele se tornar rei, Maroll nunca se livrará dos ataques de outros países.";
		mes "Quero ensiná-lo sobre o que torna este pequeno país tão poderoso e próspero. É o poder militar!";
		next;
		switch(select("Quero me juntar ao seu exército!:Encerrar Conversa")) {
		case 1:
			cutin "bat_kiyom2",2;
			mes "[General Guillaume]";
			mes "Bem-vindo ao meu exército, camarada.";
			mes "Seus olhos me dizem que você é um soldado em quem posso confiar.";
			next;
			mes "[General Guillaume]";
			mes "Agora, vá para cima e inscreva-se na batalha com seus camaradas.";
			mes "Tenho certeza de que eles o receberão de braços abertos!";
			break;
		case 2:
			mes "[General Guillaume]";
			mes "Serei aquele que capturará a bandeira!";
			break;
		}
		break;
	}
	close2;
	cutin "bat_kiyom1",255;
	cutin "bat_kiyom2",255;
	end;
}

// Teleportador para Campo de Batalha
//============================================================
bat_room,148,150,5	script	Teleportador#CampoDeBatalha	124,{
	mes "[Teleportador]";
	mes "Deseja sair do campo de batalha? Use meus serviços para retornar à cidade.";
	next;
	switch(select("Sair:Não Sair")) {
	case 1:
		mes "[Teleportador]";
		switch(bat_return) {
		default:
		case 1:
			setarray .@nome_mapa$[0],"Prontera.","prontera";
			setarray .@coordenadas[0],116,72;
			break;
		case 2:
			setarray .@nome_mapa$[0],"Ruínas de Morocc.","moc_ruins";
			setarray .@coordenadas[0],152,48;
			break;
		case 3:
			setarray .@nome_mapa$[0],"Al De Baran.","aldebaran";
			setarray .@coordenadas[0],168,112;
			break;
		case 4:
			setarray .@nome_mapa$[0],"Geffen.","geffen";
			setarray .@coordenadas[0],120,39;
			break;
		case 5:
			setarray .@nome_mapa$[0],"Payon.","payon";
			setarray .@coordenadas[0],161,58;
			break;
		case 6:
			setarray .@nome_mapa$[0],"Lighthalzen.","lighthalzen";
			setarray .@coordenadas[0],159,93;
			break;
		case 7:
			setarray .@nome_mapa$[0],"Rachel.","rachel";
			setarray .@coordenadas[0],115,124;
			break;
		case 8:
			setarray .@nome_mapa$[0],"Morocc.","morocc";
			setarray .@coordenadas[0],156,46;
			break;
		}
		mes "Você será enviado de volta para "+.@nome_mapa$[0]+".";
		close2;
		warp .@nome_mapa$[1],.@coordenadas[0],.@coordenadas[1];
		break;
	case 2:
		mes "[Teleportador]";
		mes "Estarei aqui sempre que precisar dos meus serviços.";
		close;
	}
	end;
}

-	script	Emissária de Maroll::RecrutarBatalha	728,{
	mes "[Emissária de Maroll]";
	mes "Bom dia, aventureiro.";
	mes "Sou um cavaleiro de um país distante chamado Reino de Maroll.";
	next;
	mes "[Emissária de Maroll]";
	mes "Os dois príncipes do reino estão atualmente lutando pelo trono de Maroll e precisam de soldados experientes como você.";
	mes "Como você gostaria de emprestar seu poder a um dos príncipes do Reino de Maroll?";
	next;
	switch(select("Juntar:Não Juntar")) {
	case 1:
		mes "[Emissária de Maroll]";
		mes "Que o deus da guerra o abençoe.";
		close2;
		getmapxy(.@nome_mapa$,.@x,.@y,BL_NPC);
		if (.@nome_mapa$ == "prontera")
			set bat_return,1;
		else if (.@nome_mapa$ == "moc_ruins")
			set bat_return,2;
		else if (.@nome_mapa$ == "aldebaran")
			set bat_return,3;
		else if (.@nome_mapa$ == "geffen")
			set bat_return,4;
		else if (.@nome_mapa$ == "payon")
			set bat_return,5;
		else if (.@nome_mapa$ == "lighthalzen")
			set bat_return,6;
		else if (.@nome_mapa$ == "rachel")
			set bat_return,7;
		else if (.@nome_mapa$ == "morocc")
			set bat_return,8;
		else
			set bat_return,1;
		warp "bat_room",154,150;
		break;
	case 2:
		mes "[Emissária de Maroll]";
		mes "Estarei sempre aqui para mais soldados. Sinta-se à vontade para voltar sempre que estiver interessado.";
		close;
	}
	end;
}

prontera,123,83,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha1	728
moc_ruins,75,162,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha2	728
aldebaran,146,109,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha3	728
geffen,109,66,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha4	728
payon,189,105,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha5	728
lighthalzen,153,86,5	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha6	728
rachel,149,138,3	duplicate(RecrutarBatalha)	Emissária de Maroll::RecrutarBatalha7	728

// Warps adicionais
// Vazio
bat_room,57,81,0	warp	bat1	1,1,bat_room,154,149
bat_room,57,90,0	warp	bat2	1,1,bat_room,154,149
// Empty
bat_room,85,81,0	warp	bat5	1,1,bat_room,154,149
bat_room,85,90,0	warp	bat6	1,1,bat_room,154,149
// Free BG
bat_room,113,81,0	warp	bat9	1,1,bat_room,154,149
bat_room,113,90,0	warp	bat10	1,1,bat_room,154,149
// Free BG
bat_room,141,81,0	warp	bat13	1,1,bat_room,154,149
bat_room,141,90,0	warp	bat14	1,1,bat_room,154,149
// Free BG
bat_room,169,81,0	warp	bat17	1,1,bat_room,154,149
bat_room,169,90,0	warp	bat18	1,1,bat_room,154,149
// Free BG
bat_room,197,81,0	warp	bat21	1,1,bat_room,154,149
bat_room,197,90,0	warp	bat22	1,1,bat_room,154,149
// Free BG
bat_room,225,81,0	warp	bat25	1,1,bat_room,154,149
bat_room,225,90,0	warp	bat26	1,1,bat_room,154,149
// Empty
bat_room,253,81,0	warp	bat29	1,1,bat_room,154,149
bat_room,253,90,0	warp	bat30	1,1,bat_room,154,149
// Empty
bat_room,253,220,0	warp	bat31	1,1,bat_room,154,149
bat_room,253,211,0	warp	bat32	1,1,bat_room,154,149

// Kafra
//============================================================
bat_room,148,147,4	script	Funcionário da Kafra::kaf_bat	861,{
	cutin "kafra_09",2;
	callfunc "F_Kafra",0,2,1,150,0;
}

// Consertador
//============================================================
bat_room,138,144,4	script	Consertador#bg	86,{
	callfunc "repairmain","Repairman";
	end;
}

// NPC de Gerenciamento para GMs
//============================================================
bat_room,1,151,3	script	Interruptor#batgnd	81,{
	set .@i, callfunc("F_GM_NPC",1854,0);
	if (.@i == -1) {
		mes "O comando foi cancelado.";
		close;
	} else if (.@i == 0) {
		end;
	} else {
		mes "Posso ajudar você?";
		next;
		switch(select("Fechar Campo de Batalha:Abrir Campo de Batalha:Resetar a01:Resetar b01:Resetar a02:Resetar b02")) {
		case 1:
			disablenpc "Oficial de Garganta Tierra#01a";
			disablenpc "Oficial de Garganta Tierra#02a";
			disablenpc "Oficial de Garganta Tierra#01b";
			disablenpc "Oficial de Garganta Tierra#02b";
			disablenpc "Oficial Flavius#01a";
			disablenpc "Oficial Flavius#01b";
			disablenpc "Oficial Flavius#02a";
			disablenpc "Oficial Flavius#02b";
			break;
		case 2:
			enablenpc "Oficial de Garganta Tierra#01a";
			enablenpc "Oficial de Garganta Tierra#02a";
			enablenpc "Oficial de Garganta Tierra#01b";
			enablenpc "Oficial de Garganta Tierra#02b";
			enablenpc "Oficial Flavius#01a";
			enablenpc "Oficial Flavius#01b";
			enablenpc "Oficial Flavius#02a";
			enablenpc "Oficial Flavius#02b";
			break;
		case 3:
			donpcevent "start#bat_a01::OnEnable";
			break;
		case 4:
			donpcevent "start#bat_b01::OnEnable";
			break;
		case 5:
			donpcevent "start#bat_a02::OnEnable";
			break;
		case 6:
			donpcevent "start#bat_b02::OnEnable";
			break;
		}
		mes "Completo";
		close;
	}
}

// Troca de Emblemas
//============================================================
bat_room,160,150,3	script	Erundek	109,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens consigo. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Erundek]";
	mes "Você tem emblemas de campo de batalha?";
	mes "Posso trocar emblemas de Bravura e emblemas de Valor por itens de recompensa.";
	next;
	switch(select("Trocar Emblemas:Verificar o Catálogo")) {
	case 1:
		mes "[Erundek]";
		mes "Que tipo de itens você gostaria de trocar?";
		mes "Para verificar mais informações sobre os itens de recompensa, use nosso ^3131FFCatálogo^000000.";
		next;
		switch(select("Arma:Armadura:Acessório:Consumível")) {
		case 1:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFArma^000000.";
			mes "As seguintes armas estão disponíveis para troca com os emblemas de campo de batalha.";
			mes "Observe que os itens para ^3131FFEmblemas de Bravura são indicados como (BB)^000000, e ^3131FFEmblemas de Valor como (VB)^000000.";
			next;
			switch(select("Adaga/Espada de Uma Mão/Espada de Duas Mãos/Lança de Duas Mãos:Cajado/Maça/Machado de Duas Mãos/Shuriken:Arco/Adaga Musical/Katar:Grimório/Soco Inglês:Revólver/Rifle/Metralhadora/Espingarda/Lança-granadas")) {
			case 1:
				mes "[Erundek]";
				mes "Os seguintes itens estão disponíveis na categoria ^3131FFAdaga, Espada de Uma Mão, Espada de Duas Mãos e Lança de Duas Mãos^000000.";
				next;
				setarray .@Armas[0],13036,7828,13037,7829,13411,7828,13410,7829,1183,7828,1184,7829,1425,7828,1482,7829;
				break;
			case 2:
				mes "[Erundek]";
				mes "Os seguintes itens estão disponíveis na categoria ^3131FFCajado/Maça/Machado de Duas Mãos/Shuriken^000000.";
				next;
				setarray .@Armas[0],1632,7828,1633,7829,1634,7828,1635,7829,1543,7828,1542,7829,1380,7828,1379,7829,13305,7828,13306,7829;
				break;
			case 3:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFArco/Adaga Musical/Katar^000000.";
				next;
				setarray .@Armas[0],1739,7828,1738,7829,1279,7828,1280,7829,1924,7828,1923,7829,1978,7828,1977,7829;
				break;
			case 4:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFGrimório/Soco Inglês^000000.";
				next;
				setarray .@Armas[0],1574,7828,1575,7829,1824,7828,1823,7829;
				break;
			case 5:
				mes "[Erundek]";
				mes "As seguintes armas estão disponíveis na categoria ^3131FFRevólver/Rifle/Metralhadora/Espingarda/Lança-granadas^000000.";
				next;
				setarray .@Armas[0],13108,7828,13171,7829,13172,7828,13173,7829,13174,7829;
				break;
			}
			set .@menu$,"";
			for(set .@i,0; .@i<getarraysize(.@Armas); set .@i,.@i+2)
				set .@menu$, .@menu$+getitemname(.@Armas[.@i])+((.@Armas[.@i+1]==7828)?"(BB)":"(VB)")+":";
			set .@i, (select(.@menu$)-1)*2;
			set .@tipo$, ((.@Armas[.@i+1]==7828)?"(BB)":"(VB)");
			mes "[Erundek]";
			mes "Você escolheu ^3131FF"+getitemname(.@Armas[.@i])+.@tipo$+"^000000.";
			mes "Você pode trocar por este item com ^FF0000100 "+getitemname(.@Armas[.@i+1])+"^000000.";
			mes "Gostaria de trocar?";
			next;
			switch(select("Não trocar:Trocar")) {
			case 1:
				break;
			case 2:
				mes "[Erundek]";
				mes "Gostaria de gastar ^FF0000100 "+getitemname(.@Armas[.@i+1])+"^000000 e receber um ^3131FF"+getitemname(.@Armas[.@i])+.@tipo$+"^000000?";
				next;
				mes "[Erundek]";
				mes "Lembre-se, os Itens de Recompensa do Campo de Batalha são ^FF0000Vinculados ao Personagem^000000. Você tem certeza de que deseja este item?";
				next;
				switch(select("Sim:Não")) {
				case 1:
					mes "[Erundek]";
					if (countitem(.@Armas[.@i+1]) >= 100) {
						mes "Obrigado por trocar.";
						delitem .@Armas[.@i+1],100;
						getitem .@Armas[.@i],1;
					}
					else mes "Desculpe, mas você não tem emblemas suficientes para trocar.";
					close;
				case 2:
					break;
				}
				break;
			}
			mes "[Erundek]";
			mes "Precisa de mais tempo para verificar os itens?";
			close;
		case 2:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFArmadura^000000.";
			mes "As seguintes armaduras estão disponíveis para troca com os emblemas de campo de batalha.";
			next;
			switch(select("Roupas/Calçados:Armaduras")) {
			case 1:
				setarray .@Itens[0],2538,50,2539,50,2540,50,2435,50,2436,50,2437,50;
				break;
			case 2:
				setarray .@Itens[0],2376,80,2377,80,2378,80,2379,80,2380,80,2381,80,2382,80;
				break;
			}
			break;
		case 3:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFAcessório^000000.";
			mes "Você pode trocar a Medalha de Honra por seus Emblemas de acordo com as classes de trabalho, como segue:";
			next;
			setarray .@Itens[0],2733,500,2720,500,2721,500,2722,500,2723,500,2724,500,2725,500;
			set .@menu1$,"Atirador:Espadachim/Mestre Taekwon:Ladrão/Acolyte/Mago/Arqueiro/Comerciante";
			break;
		case 4:
			mes "[Erundek]";
			mes "Você escolheu ^3131FFConsumível^000000.";
			mes "Os seguintes itens consumíveis estão disponíveis para troca com os emblemas de campo de batalha:";
			next;
			setarray .@Itens[0],12269,10,12270,10,12271,5,12272,10,12273,10;
			break;
		}
		break;
	case 2:
		mes "[Erundek]";
		mes "Temos muitos itens, então dê uma olhada e compre com cuidado.";
		close2;
		readbook 11010,1;
		end;
	}
	set .@menu$,"";
	if (.@menu1$ != "") set .@menu$, .@menu1$;
	else for(set .@i,0; .@i<getarraysize(.@Itens); set .@i,.@i+2)
		set .@menu$, .@menu$+getitemname(.@Itens[.@i])+":";
	set .@i, (select(.@menu$)-1)*2;
	mes "[Erundek]";
	mes "Você escolheu ^3131FF"+getitemname(.@Itens[.@i])+"^000000.";
	switch(.@Itens[.@i]) {
		case 2720: mes "Este item é apenas para a classe Espadachim e Mestre Taekwon."; break;
		case 2721: mes "Este item é apenas para a classe Gatuno."; break;
		case 2722: mes "Este item é apenas para a classe Sacerdote."; break;
		case 2723: mes "Este item é apenas para a classe Mago."; break;
		case 2724: mes "Este item é apenas para a classe Arqueiro."; break;
		case 2725: mes "Este item é apenas para a classe Mercador."; break;
		case 2733: mes "Este item é apenas para a classe Justiceiro."; break;
		default: break;
	}
	mes "Você pode trocar por este item com ^FF0000"+.@Itens[.@i+1]+" "+getitemname(7828)+" ou "+.@Itens[.@i+1]+" "+getitemname(7829)+"^000000.";
	mes "Gostaria de trocar?";
	next;
	switch(select("Não trocar:Trocar")) {
	case 1:
		mes "[Erundek]";
		mes "Precisa de mais tempo para verificar os itens?";
		break;
	case 2:
		mes "[Erundek]";
		mes "Qual Emblema você deseja trocar?";
		mes "Você precisa de ^3131FF"+.@Itens[.@i+1]+" Emblemas^000000 para trocar.";
		next;
		if (.@item[0] < 12269 || .@item[0] > 12273 ) {
			mes "[Erundek]";
			mes "Lembre-se, os Itens de Recompensa do Campo de Batalha são ^FF0000Vinculados ao Personagem^000000. Você tem certeza de que deseja este item?";
			next;
		}
		set .@j, select("Emblema de Bravura:Emblema de Valor:Cancelar");
		mes "[Erundek]";
		if (.@j == 3) {
			mes "Você cancelou a troca.";
			break;
		}
		set .@custo, ((.@j==1)?7828:7829);
		if (countitem(.@custo) >= .@Itens[.@i+1]) {
			mes "Obrigado por trocar.";
			delitem .@custo, .@Itens[.@i+1];
			getitem .@Itens[.@i],1;
		}
		else mes "Você não tem "+getitemname(.@custo)+"s suficientes.";
		break;
	}
	close;
}

// Função de Recompensa de Emblema
function	script	F_BG_Badge	{
	.@time$ = getarg(0);
	.@equipe$ = getarg(1);
	.@tipo_bg$ = getarg(2);

	if (.@tipo_bg$ == "Tierra") {
		set .@emblema,7828; //Emblema_Tierra
		set .@quantidade_vitoria,3;
		set .@quantidade_derrota,1;
	} else if (.@tipo_bg$ == "Flavius") {
		set .@emblema,7829; //Emblema_Flavius
		set .@quantidade_vitoria,9;
		set .@quantidade_derrota,3;
	} else
		return;

	if (.@time$ == 1) {
		set .@quantidade, .@quantidade_vitoria;
		if (.@equipe$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "Guillaume foi abençoado!";
		} else if (.@equipe$ == "Croix") {
			mes "[Swandery]";
			mes "Croix foi abençoado!";
		}
		mes "Vamos aproveitar nossa gloriosa vitória!";
		mes strcharinfo(0)+", é um sinal refletindo a vitória.";
	} else {
		set .@quantidade, .@quantidade_derrota;
		if (.@equipe$ == "Guillaume") {
			mes "[Axl Rose]";
			mes "Você perdeu, mas está dedicado a esta batalha.";
			mes "Esta é uma recompensa por sua grande dedicação de Guillaume Marollo!";
			mes "Apenas encare esta derrota como uma lição, e da próxima vez você definitivamente ganhará.";
		} else if (.@equipe$ == "Croix") {
			mes "[Swandery]";
			mes "Oh, "+strcharinfo(0)+" Não fique triste.";
			mes "Mesmo que não tenhamos vencido, fizemos o nosso melhor.";
			mes "Este é um presente real de Croix, e por favor não esqueça desta batalha. Venceremos na próxima.";
		}
	}
	close2;

	// Se o sistema VIP estiver ativado, jogadores VIP recebem mais 2 emblemas.
	if (VIP_SCRIPT && vip_status(VIP_STATUS_ACTIVE))
		set .@quantidade, .@quantidade+2;

	set .@espaco_medalha, 500 - countitem(.@emblema);
	if (.@espaco_medalha >= .@quantidade)
		getitem .@emblema,.@quantidade;
	else
		getitem .@emblema,.@espaco_medalha;
	return;
}
